!Genetic algorithm routines for single-component clusters (LJ or Morse).
!
!Generate random cluster structure
!Place all atoms inside a sphere
!
SUBROUTINE MYGA_CLUSTER_RANDOM(I)
   USE MYGA_PARAMS
   USE MYGA_POPULATION
   USE COMMONS, ONLY : NATOMS
   IMPLICIT NONE
   INTEGER CURR_STRUC
   INTEGER I,J
   DOUBLE PRECISION DPRAND
   DOUBLE PRECISION BOXSIZE,BOX2,RADIUS
   LOGICAL INSIDE_SPHERE
   BOXSIZE=MYGA_SPHERE_RADIUS*2
   BOX2=MYGA_SPHERE_RADIUS**2
   DO J=1,NATOMS
      INSIDE_SPHERE=.FALSE.
      ! Keep trying to place atom until it is inside the sphere
      DO WHILE (INSIDE_SPHERE.EQV..FALSE.)
         MYGA_POP_COORDS(3*J-2,I)=(DPRAND()-0.5)*BOXSIZE
         MYGA_POP_COORDS(3*J-1,I)=(DPRAND()-0.5)*BOXSIZE
         MYGA_POP_COORDS(3*J  ,I)=(DPRAND()-0.5)*BOXSIZE
         RADIUS = MYGA_POP_COORDS(3*J-2,I)**2+MYGA_POP_COORDS(3*J-1,I)**2+MYGA_POP_COORDS(3*J  ,I)**2
         IF (RADIUS.LT.BOX2) THEN
            INSIDE_SPHERE=.TRUE.
         ENDIF
         !WRITE(*,*) I,J,RADIUS,INSIDE_SPHERE
      END DO
   END DO
   RETURN
END
!
! Mate by 2-point crossover
!
SUBROUTINE MYGA_CLUSTER_MATE_2PT(ISTRUC,JSTRUC,OUT)
   USE MYGA_PARAMS
   USE MYGA_POPULATION
   USE COMMONS, ONLY: NATOMS
   IMPLICIT NONE
   INTEGER I
   INTEGER POS1,POS2
   INTEGER ISTRUC,JSTRUC,OUT
   DOUBLE PRECISION TMPCOOR1(3*NATOMS),TMPCOOR2(3*NATOMS)
   DOUBLE PRECISION DPRAND
   !Copy coords to temporary arrays
   IF (DPRAND().LT.0.5) THEN
      DO I = 1,3*NATOMS
         TMPCOOR1(I)=MYGA_POP_COORDS(I,ISTRUC)
         TMPCOOR2(I)=MYGA_POP_COORDS(I,JSTRUC)
      ENDDO
   ELSE
      DO I = 1,3*NATOMS
         TMPCOOR1(I)=MYGA_POP_COORDS(I,JSTRUC)
         TMPCOOR2(I)=MYGA_POP_COORDS(I,ISTRUC)
      ENDDO
   ENDIF
!Rotate parent clusters
   CALL MYGA_CLUSTER_ROTATE_RANDOM(NATOMS,TMPCOOR1)
   CALL MYGA_CLUSTER_ROTATE_RANDOM(NATOMS,TMPCOOR2)
!Sort parent clusters
   CALL MYGA_CLUSTER_Z_SORT(NATOMS,TMPCOOR1)
   CALL MYGA_CLUSTER_Z_SORT(NATOMS,TMPCOOR2)
!Pick positions for crossover
   POS1 = DPRAND()*(NATOMS-2)+1  ! Random Position 1
   POS2 = DPRAND()*(NATOMS-(POS1+1)) + POS1 + 1 ! Random Position 2
!Do crossover
!Cut and paste about positions
   DO I=1,3*POS1
      MYGA_POP_COORDS(I,OUT) = TMPCOOR1(I)
   END DO
   DO I=3*POS1+1,3*POS2
      MYGA_POP_COORDS(I,OUT) = TMPCOOR2(I)
   END DO
   DO I=3*POS2+1,3*NATOMS
      MYGA_POP_COORDS(I,OUT) = TMPCOOR1(I)
   END DO
   RETURN
END
!
! Sort atoms in order of z-coordinate
! This does not use the ga_params module to allow for operations
! on different sized sub-structures
!
SUBROUTINE MYGA_CLUSTER_Z_SORT(NATOMS,CLUSTER)
! Original implementation was x1,...,xn,y1,...,yn,z1,...,zn
! New implementation x1,y1,z1,...,xn,yn,zn
   IMPLICIT NONE
   INTEGER NATOMS
   INTEGER IR,L,I,J
   DOUBLE PRECISION CLUSTER(3*NATOMS)
   DOUBLE PRECISION TZ,TX,TY
!  Heap sort
   L=NATOMS/2+1
   IR=NATOMS
10 CONTINUE
   IF(L.GT.1)THEN
      L=L-1
      TZ=CLUSTER(3*L)
      TY=CLUSTER(3*L-1)
      TX=CLUSTER(3*L-2)
   ELSE
      TZ=CLUSTER(3*IR)
      TY=CLUSTER(3*IR-1)
      TX=CLUSTER(3*IR-2)
      CLUSTER(3*IR)=CLUSTER(3)       !Z
      CLUSTER(3*IR-1)=CLUSTER(2)     !Y
      CLUSTER(3*IR-2)=CLUSTER(1)     !X
      IR=IR-1
      IF(IR.EQ.1)THEN
         CLUSTER(3)=TZ
         CLUSTER(2)=TY
         CLUSTER(1)=TX
         RETURN
      ENDIF
   ENDIF
   I=L
   J=L+L
20 IF(J.LE.IR)THEN
      IF(J.LT.IR)THEN
         IF(CLUSTER(3*J).GT.CLUSTER(3*(J+1)))J=J+1
      ENDIF
      IF(TZ.GT.CLUSTER(3*J))THEN
         CLUSTER(3*I)=CLUSTER(3*J)     !Z
         CLUSTER(3*I-1)=CLUSTER(3*J-1) !Y
         CLUSTER(3*I-2)=CLUSTER(3*J-2) !X
         I=J
         J=J+J
      ELSE
         J=IR+1
      ENDIF
      GO TO 20
   ENDIF
   CLUSTER(3*I)=TZ
   CLUSTER(3*I-1)=TY
   CLUSTER(3*I-2)=TX
   GO TO 10
   RETURN
END
!
! Rotate a structure by random values of theta and phi
! This does not use the ga_params module to allow for operations
! on different sized sub-structures
!
SUBROUTINE MYGA_CLUSTER_ROTATE_RANDOM(NATOMS,CLUSTER)
   IMPLICIT NONE
   INTEGER NATOMS
   INTEGER I
! NOTE: FORMAT=X1,Y1,Z1,...,XN,YN,ZN
   DOUBLE PRECISION CLUSTER(3*NATOMS)
   DOUBLE PRECISION ROT11
   DOUBLE PRECISION ROT12
   DOUBLE PRECISION ROT13
   DOUBLE PRECISION ROT21
   DOUBLE PRECISION ROT22
   DOUBLE PRECISION ROT23
   DOUBLE PRECISION ROT31
   DOUBLE PRECISION ROT32
   DOUBLE PRECISION ROT33
   DOUBLE PRECISION THETA
   DOUBLE PRECISION PHI
   DOUBLE PRECISION RATOMSX
   DOUBLE PRECISION RATOMSY
   DOUBLE PRECISION RATOMSZ
   DOUBLE PRECISION DPRAND ! RANDOM NUMBER GENERATOR
!  DOUBLE PRECISION, PARAMETER :: PI = 4.d0*ATAN(1.D0)
   DOUBLE PRECISION, PARAMETER :: PI = 3.141592654D0
!  Generate random plane
   THETA = DPRAND()*(PI/2.D0) ! RANDOM NUMBER IN RANGE 2PI
   PHI = DPRAND()*PI  ! RANDOM NUMBER IN RANGE PI
! Set up rotation matrix
  ROT11 = DBLE(COS(PHI))
  ROT12 = 0.D0
  ROT13 = DBLE(-SIN(PHI))
  ROT21 = DBLE(SIN(THETA)*SIN(PHI))
  ROT22 = DBLE(COS(THETA))
  ROT23 = DBLE(SIN(THETA)*COS(PHI))
  ROT31 = DBLE(COS(THETA)*SIN(PHI))
  ROT32 = DBLE(-SIN(THETA))
  ROT33 = DBLE(COS(THETA)*COS(PHI))
  RATOMSX = 0.D0  ! SET FOR TEMP VARIABLES X
  RATOMSY = 0.D0  ! SET FOR TEMP VARIABLES Y
  RATOMSZ = 0.D0  ! SET FOR TEMP VARIABLES Z
! Rotation
  DO I=1,NATOMS
      RATOMSX = ROT11*CLUSTER(3*I-2) + ROT12*CLUSTER(3*I-1)+ ROT13*CLUSTER(3*I)
      RATOMSY = ROT21*CLUSTER(3*I-2) + ROT22*CLUSTER(3*I-1)+ ROT23*CLUSTER(3*I)
      RATOMSZ = ROT31*CLUSTER(3*I-2) + ROT32*CLUSTER(3*I-1)+ ROT33*CLUSTER(3*I)
      CLUSTER(3*I-2) = RATOMSX
      CLUSTER(3*I-1) = RATOMSY
      CLUSTER(3*I)   = RATOMSZ
   END DO
   RETURN
END

