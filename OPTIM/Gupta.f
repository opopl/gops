C   GMIN: A PROGRAM FOR FINDING GLOBAL MINIMA
C   COPYRIGHT (C) 1999-2006 DAVID J. WALES
C   THIS FILE IS PART OF GMIN.
C
C   GMIN IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C   IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C   THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C   (AT YOUR OPTION) ANY LATER VERSION.
C
C   GMIN IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL,
C   BUT WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C   MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE.  SEE THE
C   GNU GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C   YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C   ALONG WITH THIS PROGRAM; IF NOT, WRITE TO THE FREE SOFTWARE
C   FOUNDATION, INC., 59 TEMPLE PLACE, SUITE 330, BOSTON, MA  02111-1307  USA
C
C
C*************************************************************************
C
C  HERE WE CALCULATE THE GUPTA POTENTIAL AND GRADIENT
C                                        
C*************************************************************************
C
      SUBROUTINE GUPTA(NATOMS,X,V,PG,GRADT,ATOMTYPE)
      IMPLICIT NONE 
      INTEGER J1, J2, ATOMTYPE,NATOMS
      DOUBLE PRECISION X(3*NATOMS), PG, DIST, PTEMP, V(3*NATOMS),
     1       GA, GRHO(NATOMS), VTEMP, VTEMP1, VTEMP2, VTEMP3, DUMMY, 
     2       RTEMP, P, Q, R0, GXI, TWOQ, RYTOEV
      LOGICAL GRADT

      PG=0.0D0

C CLERI AND ROSATO PARAMETERS
C PRB 48, 22 (1993)
C
C FCC METALS
C 

      IF (ATOMTYPE.EQ.1) THEN

C NICKEL
        P=16.999D0
        Q=1.189D0
        GA=0.0376D0
        GXI=1.070D0

      ELSE IF (ATOMTYPE.EQ.2) THEN

C COPPER
        P=10.960D0
        Q=2.278D0
        GA=0.0855D0
        GXI=1.224D0

      ELSE IF (ATOMTYPE.EQ.3) THEN

C RHODIUM
        P=18.450D0
        Q=1.867D0
        GA=0.0629D0
        GXI=1.660D0

      ELSE IF (ATOMTYPE.EQ.4) THEN

C PALLADIUM
        P=10.867D0
        Q=3.742D0
        GA=0.1746D0
        GXI=1.718D0

      ELSE IF (ATOMTYPE.EQ.5) THEN

C SILVER
        P=10.928D0
        Q=3.139D0
        GA=0.1028D0
        GXI=1.178D0

      ELSE IF (ATOMTYPE.EQ.6) THEN

C IRIDIUM
        P=16.980D0
        Q=2.691D0
        GA=0.1156D0
        GXI=2.289D0

      ELSE IF (ATOMTYPE.EQ.7) THEN

C PLATINUM
        P=10.612D0
        Q=4.004D0
        GA=0.2975D0
        GXI=2.695D0

      ELSE IF (ATOMTYPE.EQ.8) THEN

C GOLD
        P=10.229D0
        Q=4.036D0
        GA=0.2061D0
        GXI=1.790D0

      ELSE IF (ATOMTYPE.EQ.9) THEN

C ALUMINIUM
        P=8.612D0
        Q=2.516D0
        GA=0.1221D0
        GXI=1.316D0

      ELSE IF (ATOMTYPE.EQ.10) THEN

C LEAD
        P=9.576D0
        Q=3.648D0
        GA=0.0980D0
        GXI=0.914D0
C
C HCP METALS
C 

      ELSE IF (ATOMTYPE.EQ.11) THEN

C TITANIIUM I
        P=8.620D0
        Q=2.390D0
        GA=0.1519D0
        GXI=1.8112D0

      ELSE IF (ATOMTYPE.EQ.12) THEN

C TITANIIUM II
        P=11.418D0
        Q=1.643D0
        GA=0.0741D0
        GXI=1.4163D0

      ELSE IF (ATOMTYPE.EQ.13) THEN

C ZIRCONIUM I
        P=8.250D0
        Q=2.249D0
        GA=0.1934D0
        GXI=2.2792D0

      ELSE IF (ATOMTYPE.EQ.14) THEN

C ZIRCONIUM II
        P=13.940D0
        Q=1.071D0
        GA=0.0523D0
        GXI=1.4489D0

      ELSE IF (ATOMTYPE.EQ.15) THEN

C COBALT
        P=11.604D0
        Q=2.286D0
        GA=0.0950D0
        GXI=1.4880D0

      ELSE IF (ATOMTYPE.EQ.16) THEN

C CADMIUM I
        P=10.612D0
        Q=5.206D0
        GA=0.1420D0
        GXI=0.8117D0

      ELSE IF (ATOMTYPE.EQ.17) THEN

C CADMIUM II
        P=13.639D0
        Q=3.908D0
        GA=0.0416D0
        GXI=0.4720D0

      ELSE IF (ATOMTYPE.EQ.18) THEN

C ZINC
        P=9.689D0
        Q=4.602D0
        GA=0.1477D0
        GXI=0.8900D0

      ELSE IF (ATOMTYPE.EQ.19) THEN

C MAGNESIUM
        P=12.820D0
        Q=2.257D0
        GA=0.0290D0
        GXI=0.4992D0
C
C BCC
C

      ELSE IF (ATOMTYPE.EQ.20) THEN

C VANADIUM
        P=5.206D0
        Q=1.22D0
        GA=0.6124D0
        GXI=2.441D0
      

      ELSE IF (ATOMTYPE.EQ.21) THEN

C PARAMETERS FOR SODIUM
  
        P=10.13D0
        Q=1.3D0
C R0 IN BOHR
C        R0=6.99D0
C ENERGIES IN EV
C        GA=0.015955D0
C        GXI=0.29113D0
C ENERGIES IN RYDBERG
        GA=1.1727D-3
        GXI=21.398D-3
C CONVERT TO EV
        RYTOEV=4.359748D-18/(2*1.6021773D-19)
        GA=GA*RYTOEV
        GXI=GXI*RYTOEV

      ELSE IF (ATOMTYPE.EQ.22) THEN

C STRONTIUM - WANG + BLAISTEN-BAROJAS JCP 115 3640 (2001)

        P=17.649D0
        Q=(6.0D0-1.1132D0)*(2.0D0/NATOMS)**0.65D0+1.1132D0
        GA=(0.018089D0-0.015761D0)*(2.0D0/NATOMS)**0.52588D0+0.015761D0
        GXI=(0.13977D0-0.3832D0)*(2.0D0/NATOMS)**0.8D0+0.3832D0
C       DISTANCE UNIT
C        R0=(5.6D0-4.301D0)*(2.0D0/NATOMS)**3.6D0+4.301D0

      ELSE IF (ATOMTYPE.EQ.23) THEN

C AU - AS USED BY GARZON ET AL

        P=10.15D0
        Q=4.13D0
        GA=0.118438D0/2.0D0
        GXI=0.5D0

      ENDIF

C R0 IN REDUCED UNITS
      R0=1.0D0

      TWOQ=2*Q

      DO 22 J1=1,NATOMS
         RTEMP=0.0D0
         PTEMP=0.0D0
         DO 23 J2=1,NATOMS
            IF (J1.NE.J2) THEN
               DIST=DSQRT(( X(3*(J2-1)+1)-X(3*(J1-1)+1) )**2 +
     1           ( X(3*(J2-1)+2)-X(3*(J1-1)+2) )**2 +
     2           ( X(3*(J2-1)+3)-X(3*(J1-1)+3) )**2)
               DIST=DIST/R0
               PTEMP=PTEMP+DEXP(P*(1-DIST))
               RTEMP=RTEMP+DEXP(TWOQ*(1-DIST))
            ENDIF
23       CONTINUE
         GRHO(J1)=DSQRT(RTEMP)
         PG=PG+GA*PTEMP-GXI*GRHO(J1)
22    CONTINUE

C
C NOW CALCULATE THE GRADIENT ANALYTICALLY.
C

      IF (GRADT) THEN
      DO J1=1,NATOMS
         VTEMP1=0.0D0
         VTEMP2=0.0D0
         VTEMP3=0.0D0
         DUMMY=1.0D0/GRHO(J1)
         DO J2=1,NATOMS
            IF (J1.NE.J2) THEN
               DIST=DSQRT(( X(3*(J2-1)+1)-X(3*(J1-1)+1) )**2 +
     1           ( X(3*(J2-1)+2)-X(3*(J1-1)+2) )**2 +
     2           ( X(3*(J2-1)+3)-X(3*(J1-1)+3) )**2)
               DIST=DIST/R0
               VTEMP=(Q*GXI*(DUMMY+1.0D0/GRHO(J2))*DEXP(TWOQ*(1-DIST))
     1                -2.0D0*GA*P*DEXP(P*(1-DIST)))/(R0*DIST)
               VTEMP1=VTEMP1+VTEMP*(X(3*(J1-1)+1)-X(3*(J2-1)+1))
               VTEMP2=VTEMP2+VTEMP*(X(3*(J1-1)+2)-X(3*(J2-1)+2))
               VTEMP3=VTEMP3+VTEMP*(X(3*(J1-1)+3)-X(3*(J2-1)+3))
            ENDIF
         ENDDO
         V(3*(J1-1)+1)=VTEMP1
         V(3*(J1-1)+2)=VTEMP2
         V(3*(J1-1)+3)=VTEMP3
      ENDDO
      ENDIF

      RETURN
      END

