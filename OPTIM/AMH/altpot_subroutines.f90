!-----------------------------------------
!        A L T P O T    R E A D    M E ! 
!
!   REQUIRES SMALL ALTERATIONS IN "QCHRGMK.F" AND "NNET.F" AND "INITIL.F" FROM STANDARD AMH
!     
!
!    MOST PARAMETERS ARE STORED IN MODULE "GLOBALS_ALT"
!
!    ALTPOT_MASTER IS THE ROUTINE THAT GENERALLY SHOULD BE CALLED BY A MAIN PROGRAM
!    READ_INPUT_ALT INITIALIZES THE PARAMETERS AND SHOULD BE CALLED
!    ONLY  ONCE BUT PRIOR TO ANY USE OF ALTPOT_MASTER.
!    
!    THE OTHERS ARE "SLAVES" (COMPARE WITH PRIVATE AND PUBLIC IN C++)
!
!    PUBLIC:
!     SUBROUTINE ALTPOT_MASTER
!     SUBROUTINE READ_INPUT_ALT
!    PRIVATE:
!     SUBROUTINE READ_ALTGAMMA    (SLAVE)
!     SUBROUTINE DEFAULT_ALT()    (SLAVE)
!     SUBROUTINE CALC_FORCE_ALT   (SLAVE)
!     FUNCTION   CALC_ENERGY_ALT  (SLAVE)
!     SUBROUTINE CALC_XYZ         (SLAVE)
!     SUBROUTINE CALC_THETA_ALT   (SLAVE)
!     SUBROUTINE CALC_A_ALT       (SLAVE)
!     SUBROUTINE CALC_SIGMA_ALT   (SLAVE)
!
! DESCRIPTION: ALTPOT REFERS TO POTENTIAL FUNCTIONS USED FOR DESCRIBING THE USE OF
!  INTERACTION-"GAMMA"-PARAMETERS WHOSE WEIGHTS ALTERS IN RESPONSE TO DIFFERENT ENVIRONMENTS.
!  THIS IS SUPPOSED TO MIMICK A MANY-BODY POTENTIAL AND IN PARTICULAR TO LETT GAMMAS VARY
! DEPENDING ON IF RESIDUE PAIRS ARE LOCATED IN THE CORE OR AT THE SURFACE.
! 
! VARIABLES: A(I) = DENSITY AROUND SITE I ; THETA(I,J,WELL))=NUMBER OF GUYS IJ IN WELL 
!           SIGMA=MEASURE OF HOW BURIED -> WEIGHTING FUNCTION
!
! PARAMETERS:
!  DEBUGFLAG-  SET STATICALLY DURING COMPILATION IN MODULE.
!  ALTPOTFLAG- IS SET IN FILE 'INPUT'. FORMAT=I1,I2,KEYWORD "ALTPOTFLAG" I1 AND I1 REFERS TO 
!              WELL 1 AND 2 AND NON-ZERO VALUES -> TRUE FOR THAT PARTICULAR WELL. E.G.
!              "1,1,ALTPOT" 
!  KAPPA_ALT : STEEPNESS OF STEP-FUNCTION FOR SIGMA, SET IN FILE 'INPUT' E.G. "7.0,KAPPA_ALT"
!  KAPPA_WELL : STEEPNESS OF STEP-FUNCTIONS FOR WELLS, SET IN FILE 'INPUT' E.G. "7.0,KAPPA_WELL"
!  TRESHOLD_ALT : TRESHOLD VALUE FOR CONTACTS DETERMINES WETHER IN OR OUT.E.G. "3,TRESHOLD_ALT"
!  KAPPA_OB : STEEPNESS OF THE WELLS FOR THE ONEBODYPOTENTIAL. SET IN 'INPUT', KEYWORD "KAPPA_OB"
!  ONEBODYFLAG,ONEBODY_TYPE : FF FLAG IS NOT 0 -> ONEBODYPOT INCLUDED.ONEBODY_TYPE MAKES FOR DIFFERENT
!                            TYPES OF OB_POT, SEE "ONEBODY.F90"  KEYWORD "ONEBODYFLAG"
!
! DATA INPUT: 
!       GAMMA.DAT: 1                  2              3             4       
!       GAMMA.DAT: GAMMA(DIRECT) GAMMA(EXPOSED)  I_WELL            KEYWORD="ALTPOT" SOMEWHERE IN LINE
!       OENBODY            1                        2
!       GAMMA.DAT: ONEBODYGAMMA(EXPOSED)   ONEBODYGAMMA(BURIED)   KEYWORD="ONEBODY" SOMEWHERE IN LINE
!-----------------------------------------

      SUBROUTINE ALTPOT_MASTER(PRO_CORD,F_CORD,E,TEMPAV,NMRES)
  
        USE AMHGLOBALS,  ONLY : AMHMAXSIZ,MAXCRD,R_MIN,R_MAX, IRES
        USE GLOBALS_ALT, ONLY : DEBUG_NUMER_FORCES, DEBUGFLAG,KAPPA_ALT,TRESHOLD_ALT &
          ,ALTPOTFLAG,MAX_WELL_ALT,KAPPA_WELL, DO_SEND_OUTPUT , ONEBODYFLAG, ACCUMULATED_TIME

        USE ALTPOT_INTERFACES, ONLY: CALC_XYZ, CALC_THETA_ALT, CALC_A_ALT, CALC_SIGMA_ALT, &
             CALC_FORCE_ALT, CALC_ENERGY_ALT, CALC_ONEBODY_FORCE, CALC_ONEBODY_POT, &
             SEND_OUTPUT_ALT, CALC_NUMERICAL_FORCE_ALT

        IMPLICIT NONE
        INTEGER, INTENT(IN):: NMRES
         DOUBLE PRECISION, INTENT(IN) ::  PRO_CORD(AMHMAXSIZ,3,MAXCRD)
        LOGICAL, INTENT(IN):: TEMPAV
        !       DOUBLE PRECISION, INTENT(OUT) :: F_CORD(AMHMAXSIZ,3,MAXCRD),E
        DOUBLE PRECISION F_CORD(AMHMAXSIZ,3,MAXCRD),E

        ! LOCAL VARIABLES:
        INTEGER I,J,I_WELL,MAXLOOP
        DOUBLE PRECISION START_TIME, END_TIME1, END_TIME2
        DOUBLE PRECISION XYZ_DIST(AMHMAXSIZ,AMHMAXSIZ),XYZ_UNIT_VECT(AMHMAXSIZ,AMHMAXSIZ,3)
        DOUBLE PRECISION THETA(AMHMAXSIZ,AMHMAXSIZ,MAX_WELL_ALT),THETA_DOT(AMHMAXSIZ,AMHMAXSIZ,MAX_WELL_ALT)
        DOUBLE PRECISION A(AMHMAXSIZ),SIGMA(AMHMAXSIZ,AMHMAXSIZ),E_ALT(2,MAX_WELL_ALT),E_OB(3) 

        INTEGER IATOM
        DOUBLE PRECISION PP_1_F_CORD(AMHMAXSIZ,3,MAXCRD), PP_2_F_CORD(AMHMAXSIZ,3,MAXCRD), OB_F_CORD(AMHMAXSIZ,3,MAXCRD)
        DOUBLE PRECISION F_ALT_NUMER_PAIR_POT(AMHMAXSIZ,3,MAXCRD), F_ALT_NUMER_OB_POT(AMHMAXSIZ,3,MAXCRD)

!        EXTERNAL CPU_TIME

        CALL CPU_TIME(START_TIME)

        F_CORD=0.0D0
        E=0.0D0
        E_ALT=0.0D0
        E_OB=0.0D0
        PP_1_F_CORD=0.D0;
        PP_2_F_CORD=0.D0;

        !                                START SEGMENT_ALTPOTCALC
        !        IMPORTANT!!! NOTE THE ORDERING IS NOT ARBITRARY AND SHOULD BE:      IMPORTANT!!! 
        !        CALC_XYZ , CALC_THETA , CALC_A , CALC_SIGMA AND CALC_FORCE
        ! 
        IF((ALTPOTFLAG(1).GT.0).OR.(ALTPOTFLAG(2).GT.0))THEN
           CALL CALC_XYZ (XYZ_DIST,XYZ_UNIT_VECT,PRO_CORD,NMRES)

           CALL CALC_THETA_ALT(THETA,THETA_DOT,XYZ_DIST,R_MIN(1),R_MAX(1),KAPPA_WELL,NMRES,1)
           IF(ALTPOTFLAG(2).GT.0)THEN
              CALL CALC_THETA_ALT(THETA,THETA_DOT,XYZ_DIST,R_MIN(2),R_MAX(2),KAPPA_WELL,NMRES,2)
           ENDIF
           CALL CALC_A_ALT(A,THETA,NMRES)
           CALL CALC_SIGMA_ALT(SIGMA,A,TRESHOLD_ALT,KAPPA_ALT,NMRES) ! KAPPA OK
        ENDIF

        CALL CPU_TIME(END_TIME1)
        ACCUMULATED_TIME(10)=ACCUMULATED_TIME(10)+(END_TIME1-START_TIME)

        MAXLOOP=1                        ! NOTE HARDWIRE!!  MAX_WELL IS  
        IF(ALTPOTFLAG(2).GT.0)MAXLOOP=2  !  CURRENTLY 2 , NEED BE CHANGED FOR MORE WELLS
        DO I_WELL=1,MAXLOOP,1
           IF(ALTPOTFLAG(I_WELL).GT.0)THEN
              CALL CALC_FORCE_ALT &
                   (F_CORD,PRO_CORD,THETA,THETA_DOT,SIGMA,XYZ_UNIT_VECT,NMRES,I_WELL &
                   ,A, KAPPA_ALT, TRESHOLD_ALT)
              IF(TEMPAV.OR.DO_SEND_OUTPUT)E=E+CALC_ENERGY_ALT(THETA,SIGMA,E_ALT,NMRES,I_WELL)
           ENDIF
           IF(DO_SEND_OUTPUT .AND. DEBUG_NUMER_FORCES) THEN 
              IF(I_WELL .EQ. 1) PP_1_F_CORD=F_CORD
              IF(I_WELL .EQ. 2) PP_2_F_CORD=F_CORD-PP_1_F_CORD
           ENDIF
        ENDDO

        !                             ONE BODY POTENTIAL

        IF(ONEBODYFLAG.EQ.1)THEN
           CALL CALC_ONEBODY_FORCE(F_CORD,THETA_DOT,XYZ_UNIT_VECT,NMRES,A)
           IF(TEMPAV.OR.DO_SEND_OUTPUT)E=E+CALC_ONEBODY_POT(A,NMRES,E_OB)
        ENDIF

        !                             END SEGMENT_ALTPOTCALC


        !DEBUG FORCES:
        IF(DO_SEND_OUTPUT .AND. DEBUG_NUMER_FORCES) THEN
           CALL CALC_NUMERICAL_FORCE_ALT(PRO_CORD,F_ALT_NUMER_PAIR_POT,F_ALT_NUMER_OB_POT,NMRES)
           OPEN(UNIT=1282,FILE='ALT_NUMER_FORCE_PP',STATUS='UNKNOWN')  
           OPEN(UNIT=1283,FILE='ALT_ANALYT_FORCE_PP',STATUS='UNKNOWN')  
           IF(ONEBODYFLAG.EQ.1)THEN
              OB_F_CORD=F_CORD-PP_1_F_CORD-PP_2_F_CORD
              OPEN(UNIT=1284,FILE='ALT_NUMER_FORCE_OB',STATUS='UNKNOWN')  
              OPEN(UNIT=1285,FILE='ALT_ANALYT_FORCE_OB',STATUS='UNKNOWN')  
           ENDIF
           DO I=1,NMRES
              IATOM=2
              IF (IRES(I) .EQ. 8) IATOM=1
              WRITE(1282,100) (F_ALT_NUMER_PAIR_POT(I,J,IATOM),J=1,3)
              WRITE(1283,100) (PP_2_F_CORD(I,J,IATOM),J=1,3)
              IF(ONEBODYFLAG.EQ.1)THEN
                 WRITE(1284,100) (F_ALT_NUMER_OB_POT(I,J,IATOM),J=1,3)
                 WRITE(1285,100) (OB_F_CORD(I,J,IATOM),J=1,3)
              ENDIF
           ENDDO
           CLOSE(1282)
           CLOSE(1283)
           IF(ONEBODYFLAG.EQ.1)THEN
              CLOSE(1284)
              CLOSE(1285)
           ENDIF
100        FORMAT(400(1X,E12.6))
        ENDIF

        CALL CPU_TIME(END_TIME2)
        ACCUMULATED_TIME(1)=ACCUMULATED_TIME(1)+(END_TIME2-START_TIME)

        CALL SEND_OUTPUT_ALT(A,E_ALT,NMRES,E_OB)

      END SUBROUTINE ALTPOT_MASTER

      ! THIS IS FOR DEBUGGING ANALYTICAL FORCES
      SUBROUTINE CALC_NUMERICAL_FORCE_ALT (PRO_CORD,F_ALT_NUMER_PAIR_POT,F_ALT_NUMER_OB_POT,NMRES)

        USE AMHGLOBALS,  ONLY : AMHMAXSIZ,MAXCRD,R_MIN,R_MAX, IRES
        USE GLOBALS_ALT, ONLY : KAPPA_ALT,TRESHOLD_ALT &
             ,ALTPOTFLAG,MAX_WELL_ALT,KAPPA_WELL, ONEBODYFLAG,ACCUMULATED_TIME

        USE ALTPOT_INTERFACES, ONLY: CALC_XYZ, CALC_THETA_ALT, CALC_A_ALT, CALC_SIGMA_ALT, &
                           CALC_FORCE_ALT, CALC_ENERGY_ALT, CALC_ONEBODY_FORCE, CALC_ONEBODY_POT

        IMPLICIT NONE
        INTEGER, INTENT(IN) :: NMRES
        DOUBLE PRECISION PRO_CORD(AMHMAXSIZ,3,MAXCRD)
         DOUBLE PRECISION, INTENT(OUT) :: F_ALT_NUMER_PAIR_POT(AMHMAXSIZ,3,MAXCRD)
         DOUBLE PRECISION, INTENT(OUT) ::  F_ALT_NUMER_OB_POT(AMHMAXSIZ,3,MAXCRD)


        ! LOCAL VARIABLES (JU):
        DOUBLE PRECISION XYZ_DIST(AMHMAXSIZ,AMHMAXSIZ),XYZ_UNIT_VECT(AMHMAXSIZ,AMHMAXSIZ,3)
        DOUBLE PRECISION THETA(AMHMAXSIZ,AMHMAXSIZ,MAX_WELL_ALT), THETA_DOT(AMHMAXSIZ,AMHMAXSIZ,MAX_WELL_ALT)
        DOUBLE PRECISION A(AMHMAXSIZ),SIGMA(AMHMAXSIZ,AMHMAXSIZ),E_ALT(2,MAX_WELL_ALT),E_OB(3)

        ! LOCAL VARIABLES (GAP):

        INTEGER, PARAMETER :: R16 = SELECTED_REAL_KIND(16, 50) ! AT LEAST 16 DECIMAL DIGITS OF PRECISION AND THE EXPONENT RANGE 200
        !      DOUBLE PRECISION (KIND = R16) :: E_PAIR_POT(2), E_OB_POT(2)
        DOUBLE PRECISION :: E_PAIR_POT(2), E_OB_POT(2)
         DOUBLE PRECISION, PARAMETER :: STEP_SIZE=0.005D0

        INTEGER K,KATOM,XYZ,STEP

        INTEGER, PARAMETER :: WELL=2 !NOTE: 2ND WELL IS HARDWIRED FOR NUMERICAL FORCES, SEE ALSO PRINTING IN ALTPOT_MASTER

        DOUBLE PRECISION SAVE_PRO_CORD(AMHMAXSIZ,3,MAXCRD), SAVED_CORD, START_TIME, END_TIME

!        EXTERNAL CPU_TIME

        CALL CPU_TIME(START_TIME)


        SAVE_PRO_CORD=PRO_CORD

        F_ALT_NUMER_PAIR_POT=0.D0
        F_ALT_NUMER_OB_POT=0.D0

        DO K=1,NMRES
           KATOM=2
           IF (IRES(K) .EQ. 8) KATOM=1
           DO XYZ=1,3
              PRO_CORD=SAVE_PRO_CORD
              SAVED_CORD=PRO_CORD(K,XYZ,KATOM)
              DO STEP=-1,1,2
                 PRO_CORD(K,XYZ,KATOM)=SAVED_CORD+STEP_SIZE*STEP

                 E_ALT=0.0D0
                 E_OB=0.0D0

                 CALL CALC_XYZ (XYZ_DIST,XYZ_UNIT_VECT,PRO_CORD,NMRES)

                 CALL CALC_THETA_ALT(THETA,THETA_DOT,XYZ_DIST,R_MIN(1),R_MAX(1),KAPPA_WELL,NMRES,1)
                 CALL CALC_THETA_ALT(THETA,THETA_DOT,XYZ_DIST,R_MIN(2),R_MAX(2),KAPPA_WELL,NMRES,2)
                 CALL CALC_A_ALT(A,THETA,NMRES)
                 CALL CALC_SIGMA_ALT(SIGMA,A,TRESHOLD_ALT,KAPPA_ALT,NMRES) 

                 IF(ALTPOTFLAG(WELL).GT.0)THEN
                    E_PAIR_POT((STEP+3)/2)=CALC_ENERGY_ALT(THETA,SIGMA,E_ALT,NMRES,WELL)
                 ENDIF

                 IF(ONEBODYFLAG.EQ.1)THEN
                    E_OB_POT((STEP+3)/2)=CALC_ONEBODY_POT(A,NMRES,E_OB)
                    !                  WRITE(*,*) "NUMDIFF 2: ",K,XYZ,STEP,E_OB_POT((STEP+3)/2)
                 ENDIF

              ENDDO

              IF(ALTPOTFLAG(WELL).GT.0)THEN
                 F_ALT_NUMER_PAIR_POT(K,XYZ,KATOM)=-1*(E_PAIR_POT(2)-E_PAIR_POT(1))/2/STEP_SIZE
                 !           WRITE(*,664) WELL,K,XYZ,E_PAIR_POT(1),E_PAIR_POT(2),F_ALT_NUMER_PAIR_POT(K,XYZ,KATOM)
                 !664        FORMAT('NUMDIFF PP: WELL=',I3,' K=', I3,' XYZ=',I3,' E1=',E16.8,' E2=',E16.8,' F=',E16.8)
              ENDIF
              IF(ONEBODYFLAG.EQ.1)THEN
                 F_ALT_NUMER_OB_POT(K,XYZ,KATOM)=-1*(E_OB_POT(2)-E_OB_POT(1))/2/STEP_SIZE
                 !           WRITE(*,665) K,XYZ,E_OB_POT(1),E_OB_POT(2),F_ALT_NUMER_OB_POT(K,XYZ,KATOM)
                 !665        FORMAT('NUMDIFF OB: K=', I3,' XYZ=',I3,' E1=',E16.8,' E2=',E16.8,' F=',E16.8)
              ENDIF
           ENDDO
        ENDDO

        PRO_CORD=SAVE_PRO_CORD

        CALL CPU_TIME(END_TIME)
        ACCUMULATED_TIME(4) = ACCUMULATED_TIME(4) + (END_TIME-START_TIME)

        RETURN
      END SUBROUTINE CALC_NUMERICAL_FORCE_ALT

