! LEFORESTIER 2002 VRT(MCY-5F)
! X(I,J) = ITH DIMENSION OF JTH ATOM
! O IS 1, H IS 2 AND 3
! DISTANCES IN A.U., ENERGIES IN A.U.

MODULE MCY

IMPLICIT NONE

! PARAMETERS IN A.U.
REAL*8, PARAMETER :: Q2 = 0.583599D0,&
							AOO = 2376.94D0,&
							BOO = 2.71770D0,&
							AHH = 0.98642D0,&
							BHH = 1.56704D0,&
							AOH = 2.91318D0,&
							BOH = 1.50467D0,&
							AVH = 0.475543D0,&
							BVH = 1.18216D0,&
							DVC = 0.517424D0,&
							DVS = 0.03857D0

CONTAINS

!#####################################

REAL*8 FUNCTION POTENTIAL(COORDS)
REAL*8, INTENT(IN) :: COORDS(:)
INTEGER :: I
REAL*8 :: X(3,6), R1(3), R2(3), Q1, Q2, THETA, POT
X = RESHAPE(COORDS,(/3,6/))
POTENTIAL = INTER(X)
DO I = 0, 1
	R1 = X(:,2+I*3) - X(:,1+I*3)
	R2 = X(:,3+I*3) - X(:,1+I*3)
	Q1 = SQRT(SUM(R1**2))
	Q2 = SQRT(SUM(R2**2))
	THETA = ACOS(DOT_PRODUCT(R1, R2) / (Q1*Q2))
	CALL POTS(POT, Q1, Q2, THETA)
	POTENTIAL = POTENTIAL + POT
END DO
END FUNCTION POTENTIAL

!#####################################

REAL*8 FUNCTION INTER(X)
REAL*8, INTENT(IN) :: X(3,6)
INTEGER :: I, J
REAL*8 :: XN(3,2), XV(3,2), R, R1(3), R2(3)

INTER = 0.0D0

!CALCULATE POSITION OF NEGATIVE/VIRTUAL SITES

R1 = X(:,2) + X(:,3) - 2.0D0*X(:,1)
R1 = R1 / SQRT(SUM(R1**2))
R2 = X(:,5) + X(:,6) - 2.0D0*X(:,4)
R2 = R2 / SQRT(SUM(R2**2))

XN(:,1) = X(:,1) + DVC*R1
XN(:,2) = X(:,4) + DVC*R2
XV(:,1) = X(:,1) + DVS*R1
XV(:,2) = X(:,4) + DVS*R2

!SUM OVER INTERATOMIC COULOMBIC INTERACTIONS AND DISPERSION TERMS

!BETWEEN NEGATIVE SITES
R = SQRT(SUM((XN(:,1) - XN(:,2))**2))
INTER = INTER + 4*Q2/R

!BETWEEN OXYGEN ATOMS
R = SQRT(SUM((X(:,1) - X(:,4))**2))
INTER = INTER + AOO * EXP(-BOO*R)

DO I = 2, 3 ! HYDROGENS ON MOLECULE 1
	DO J = 5, 6 ! HYDROGENS ON MOLECULE 2
		R = SQRT(SUM((X(:,I) - X(:,J))**2))
		INTER = INTER + Q2/R + AHH*EXP(-BHH*R)
	END DO
END DO

DO I = 1, 2 ! NEGATIVE/VIRTUAL SITES
	DO J = 2, 3 ! HYDROGENS
		R = SQRT(SUM((XN(:,I) - X(:,J+6-I*3))**2))
		INTER = INTER - 2*Q2/R
		R = SQRT(SUM((XV(:,I) - X(:,J+6-I*3))**2))
		INTER = INTER - AVH*EXP(-BVH*R)
	END DO
END DO

DO I = 1, 4, 3 ! OXYGEN ATOMS
	DO J = 2, 3 ! HYDROGENS
		R = SQRT(SUM((X(:,I) - X(:,J+4-I))**2))
		INTER = INTER + AOH*EXP(-BOH*R)
	END DO
END DO

END FUNCTION INTER

!#####################################

END MODULE MCY
