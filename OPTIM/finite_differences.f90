MODULE FINITE_DIFFERENCES

IMPLICIT NONE

CONTAINS

!###############################

FUNCTION FINDIFGRAD(X, POT, EPS, DOUBLE)
DOUBLE PRECISION, INTENT(IN) :: X(:), EPS
LOGICAL, INTENT(IN) :: DOUBLE ! BETTER ACCURACY GRADIENT
INTERFACE
	DOUBLE PRECISION FUNCTION POT(X)
	DOUBLE PRECISION, INTENT(IN), DIMENSION(:) :: X
	END FUNCTION POT
END INTERFACE
DOUBLE PRECISION, DIMENSION(SIZE(X)) :: FINDIFGRAD, EI
INTEGER :: I

EI = 0.0
IF (DOUBLE) THEN
	DO I = 1, SIZE(X)
		EI(I) = EPS
		FINDIFGRAD(I) = (-POT(X+2*EI) + 8*POT(X+EI) - 8*POT(X-EI) + POT(X-2*EI)) / (12*EPS)
		EI(I) = 0.0
	END DO
ELSE
	DO I = 1, SIZE(X)
		EI(I) = EPS
		FINDIFGRAD(I) = (POT(X+EI) - POT(X-EI)) / (2*EPS)
		EI(I) = 0.0
	END DO
END IF
END FUNCTION FINDIFGRAD

!###############################

FUNCTION FINDIFHESS(X, G, GRAD, EPS) RESULT(HESS)
DOUBLE PRECISION, INTENT(IN) :: X(:), G(:), EPS
INTERFACE
	FUNCTION GRAD(X)
	DOUBLE PRECISION, INTENT(IN), DIMENSION(:) :: X
	DOUBLE PRECISION, DIMENSION(SIZE(X)) :: GRAD
	END FUNCTION GRAD
END INTERFACE
DOUBLE PRECISION, DIMENSION(SIZE(X)) :: X0
DOUBLE PRECISION, DIMENSION(SIZE(X),SIZE(X)) :: HESS
INTEGER :: I

X0 = X
DO I = 1, SIZE(X)
	X0(I) = X(I) + EPS
	HESS(I,:) = (GRAD(X0) - G) / EPS
	X0(I) = X(I)
END DO
CALL SYMMETRIZE(HESS)
END FUNCTION FINDIFHESS

!###############################

FUNCTION FINDIFHESS_POT(X, POT, EPS) RESULT(HESS)
DOUBLE PRECISION, INTENT(IN) :: X(:), EPS
INTERFACE
	DOUBLE PRECISION FUNCTION POT(X)
	DOUBLE PRECISION, INTENT(IN), DIMENSION(:) :: X
	END FUNCTION POT
END INTERFACE
DOUBLE PRECISION :: V0
DOUBLE PRECISION, DIMENSION(SIZE(X)) :: EI, EJ
DOUBLE PRECISION, DIMENSION(SIZE(X),SIZE(X)) :: HESS
INTEGER :: I, J

V0 = POT(X)

EI = 0.0
EJ = 0.0

DO I = 1, SIZE(X)
   EI(I) = EPS
   HESS(I,I) = (-POT(X+2*EI) + 16*POT(X+EI) - 30*V0 + 16*POT(X-EI) - POT(X-2*EI)) / (12*EPS**2)
   DO J = 1, I-1
      EJ(J) = EPS
!     HESS(I,J) = (POT(X+EI+EJ) - POT(X-EI+EJ) - POT(X+EI-EJ) + POT(X-EI-EJ)) / (2*EPS)**2
      HESS(I,J) = (-POT(X+2*EI+2*EJ) + 16*POT(X+EI+EJ) - 30*V0 + 16*POT(X-EI-EJ) - POT(X-2*EI-2*EJ)) / &
  &   (24*EPS**2) - (HESS(I,I) + HESS(J,J)) / 2
      HESS(J,I) = HESS(I,J)
      EJ(J) = 0.0
   END DO
   EI(I) = 0.0
END DO
END FUNCTION FINDIFHESS_POT

!###############################

SUBROUTINE SYMMETRIZE(HESS)
DOUBLE PRECISION, INTENT(INOUT) :: HESS(:,:)
INTEGER :: I, J, N
DOUBLE PRECISION :: TMP
N = SIZE(HESS,1)
DO I = 1, N
	DO J = I+1, N
		TMP = 0.5 * (HESS(I,J) + HESS(J,I))
		HESS(I,J) = TMP
		HESS(J,I) = TMP
	END DO
END DO
END SUBROUTINE SYMMETRIZE

!###############################

END MODULE FINITE_DIFFERENCES
