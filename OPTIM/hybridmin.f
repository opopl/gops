C   OPTIM: A PROGRAM FOR OPTIMIZING GEOMETRIES AND CALCULATING REACTION PATHWAYS
C   COPYRIGHT (C) 1999-2006 DAVID J. WALES
C   THIS FILE IS PART OF OPTIM.
C
C   OPTIM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C   IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C   THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C   (AT YOUR OPTION) ANY LATER VERSION.
C
C   OPTIM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL,
C   BUT WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C   MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE.  SEE THE
C   GNU GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C   YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C   ALONG WITH THIS PROGRAM; IF NOT, WRITE TO THE FREE SOFTWARE
C   FOUNDATION, INC., 59 TEMPLE PLACE, SUITE 330, BOSTON, MA  02111-1307  USA
C
C***********************************************************************
C
C  THIS SUBROUTINE IS DESIGNED TO PERFORM A HYBRID EIGENVECTOR-FOLLOWING
C  OPTIMIZATION FOR LARGE SYSTEMS WHERE WE DO NOT WANT TO DIAGONALIZE THE WHOLE HESSIAN.
C  IN CONTRAST TO BFGSTS.F, IN THIS ROUTINE WE GO DOWNHILL.
C
C***********************************************************************
C
      SUBROUTINE HYBRIDMIN(ITMAX,COORDS,ENERGY,GRAD,MFLAG,RMS,EVALMIN,EVALMAX,VECTS,ITER,POTCALL,PTEST)
      USE COMMONS
      USE KEY
      USE VECCK
      USE ZWK
      USE MODCHARMM
      USE MODHESS
      USE PORFUNCS
      IMPLICIT NONE

      INTEGER J1, J2, INEG, J, ITER, NS, I, K1, NBFGS, ITMAX, ITDONE, J3
      DOUBLE PRECISION GRAD(3*NATOMS),ENERGY,COORDS(3*NATOMS),SUM,AVG,FOBNEW,DUMMY,
     1                 RMS,PSTEP,DPRAND, EVALMAX,EVALMIN,STPMAG,VECSP(3*NATOMS),ESAVE,
     3                 SCALE,RMS2,VECS(3*NATOMS),SOVER,EREAL,XRAT(3*NATOMS),VECTS(3*NATOMS),
     4                 XFOB(3*NATOMS),STEP(3*NATOMS),SAVECOORDS(3*NATOMS),CSTEP(3*NATOMS),
     5                 RAT1,RAT2,TEMP,LP1,LP2,LP,VECL(3*NATOMS), DOTOPT
      DOUBLE PRECISION DELTAS, DELTAT, DIAGEXP(3*NATOMS), DELTASP, TPAR
      CHARACTER(LEN=80) :: FNAME
      LOGICAL MFLAG, AWAY, PVFLAG, RESET, STEST, POTCALL, PTEST, CONVERGED
      COMMON /PVF/ PVFLAG
C
C  ASSIGN ENOUGH MEMORY TO WORK FOR A BLOCKSIZE OF 32 TO BE POSSIBLE.
C  THIS IS FOR DSYEVR.
C
      INTEGER IWORK(33*3*NATOMS)
      INTEGER ILWORK, LWORK, NFOUND, ISUPPZ(2*3*NATOMS)
      INTEGER INFO, ISTAT, NEVSSAVE, NEVLSAVE
      DOUBLE PRECISION WORK(33*3*NATOMS), ABSTOL, DIAG(3*NATOMS), DLAMCH, CEIGSAVE
      LOGICAL KNOWE, KNOWG, KNOWH
      COMMON /KNOWN/ KNOWE, KNOWG, KNOWH

      LWORK=33*3*NATOMS
      ILWORK=33*3*NATOMS
      IF (ZSYM(1)(1:1).EQ.'W') THEN
         PRINT*,'HYBRIDMIN PROCEDURES HAVE NOT BEEN PROGRAMMED FOR TIP POTENTIALS'
         CALL FLUSH(6,ISTAT)
         STOP
      ENDIF
      IF ((NUSEEV.GT.1).AND.(.NOT.NOIT.OR.NOHESS)) THEN
         WRITE(*,'(A)') 'FOR NUSEEV > 1 YOU MUST USE A HESSIAN AND NOIT WITH HYBRIDMIN'
         CALL FLUSH(6,ISTAT)
         STOP
      ENDIF
!
! CHECK FOR CONSISTENT CONVERGENCE CRITERIA.
!
      IF (GMAX.NE.CONVR) THEN
         PRINT '(2(A,G20.10),A)','BFGSTS> WARNING - GMAX ',GMAX,' IS DIFFERENT FROM CONVR ',CONVR,' - RESETTING'
         GMAX=MIN(CONVR,GMAX)
         CONVR=MIN(CONVR,GMAX)
      ENDIF
!
!  RESET MAXIMUM STEP SIZES IN CASE THIS ISN'T THE FIRST CALL TO EFOL.
!
      IF (DEBUG) PRINT '(A,G20.10)',' HYBRIDMIN> RESETTING MAXIMUM STEP SIZES TO ',HMMXSTP
      STPMAX(1:NOPT)=HMMXSTP

      ITER=1
      CONVERGED=.TRUE.
      VECS(1:NOPT)=VECTS(1:NOPT) ! SO THAT VECTS IS NOT OVERWRITTEN
C
C  IF DUMPV IS .TRUE. THEN VECTOR.DUMP IS ALREADY OPEN AND ATTACHED TO UNIT 44.
C  SGI COMPILER WON;T ALLOW US TO ATTACH IT TO ANOTHER UNIT.
C  VECTOR.DUMP COULD CONTAIN MULTIPLE DUMPS FOR MORE THAN THE LAST STEP, SO WE 
C  HAVE TO MAKE SURE WE GET THE RESULTS FOR THE LAST STEP.
C
      IF (READV.AND.(ITER.EQ.1)) THEN
         IF (DUMPV) THEN
            REWIND(44)
211         READ(44,*,END=111) EVALMIN
            READ(44,*) (VECS(J1),J1=1,NOPT)
            GOTO 211
111         CONTINUE
         ELSE
            IF (FILTH.EQ.0) THEN
               FNAME='VECTOR.DUMP'
            ELSE
               WRITE(FNAME,'(A)') 'VECTOR.DUMP.'//TRIM(ADJUSTL(FILTHSTR))
            ENDIF
   
            OPEN(UNIT=45,FILE=FNAME,STATUS='OLD')
20          READ(45,*,END=10) EVALMIN
            READ(45,*) (VECS(J1),J1=1,NOPT)
            GOTO 20
10          CLOSE(45)
            DIAG(1)=EVALMIN
         ENDIF
         WRITE(*,'(A,F20.10)') ' HYBRIDMIN> REACTION VECTOR READ SUCCESSFULLY. EIGENVALUE=   ',EVALMIN
         NS=100
      ENDIF
      IF (FREEZE) THEN
         DO J1=1,NATOMS
            IF (FROZEN(J1)) THEN
               VECS(3*(J1-1)+1)=0.0D0
               VECS(3*(J1-1)+2)=0.0D0
               VECS(3*(J1-1)+3)=0.0D0
            ENDIF
         ENDDO
      ENDIF

      STEST=.TRUE.
      KNOWG=.FALSE.
      IF (NOHESS) STEST=.FALSE.
      IF (READV.AND.(ITER.EQ.1).AND.(NUSEEV.LE.1)) STEST=.FALSE.
90    IF (PTEST) PRINT*
      NUP=NUSEEV
      IF (PTEST) WRITE(*,11) ITER
11          FORMAT (' HYBRIDMIN> BEGINNING OF OPTIMIZATION CYCLE ', I4,'.',/
     1              ' ---------------------------------------')
      FIXIMAGE=.FALSE.
      IF ((FIXAFTER.GT.0).AND.(ITER.GT.FIXAFTER)) FIXIMAGE=.TRUE.
      IF (CHRMMT) NCHENCALLS = 999 ! MAKE SURE NON-BOND LIST IS UPDATED AT THE START OF EACH TS SEARCH.
      IF (POTCALL) THEN
         IF (PV) THEN
            IF (.NOT.KNOWE) CALL POTENTIAL(COORDS,ENERGY,GRAD,.FALSE.,.FALSE.,RMS,.FALSE.,.FALSE.)
            PVFLAG=.FALSE.
            CALL PVOPT(COORDS,ENERGY,GRAD)
         ENDIF
         IF ((.NOT.KNOWG).OR.(STEST.AND.(.NOT.KNOWH))) CALL POTENTIAL(COORDS,ENERGY,GRAD,.TRUE.,STEST,RMS,PTEST,.FALSE.)
      ENDIF
      IF ((.NOT.VARIABLES).AND.NOIT.AND.STEST) THEN
         IF (ZSYM(NATOMS).EQ.'SY') THEN
            CALL SHIFTSTOCK(COORDS,NATOMS)
         ELSEIF (ZSYM(NATOMS).EQ.'TH') THEN
            CALL SHIFTHTH(COORDS,NATOMS)
         ELSE
            CALL SHIFTH(COORDS,.TRUE.,NOPT,NATOMS,ATMASS)
            IF (TWOD) THEN
               DO J1=1,NATOMS
                  J2=3*J1
                  DO J3=1,NATOMS
                     HESS(J2,3*(J3-1)+1)=0.0D0
                     HESS(J2,3*(J3-1)+2)=0.0D0
                     HESS(J2,3*(J3-1)+3)=0.0D0
                     HESS(3*(J3-1)+1,J2)=0.0D0
                     HESS(3*(J3-1)+2,J2)=0.0D0
                     HESS(3*(J3-1)+3,J2)=0.0D0
                  ENDDO
                  HESS(J2,J2)=SHIFTV
               ENDDO
            ENDIF
         ENDIF
      ENDIF
      DO I=1,NOPT
         SAVECOORDS(I)=COORDS(I)
      ENDDO
      IF (POTCALL) THEN
         IF (.NOT.NOHESS) THEN 
            IF (.NOT.NOIT) THEN
               CEIGSAVE=CEIG; CEIG=HMCEIG; NEVSSAVE=NEVS; NEVS=HMNEVS; NEVLSAVE=NEVL; NEVL=HMNEVL ! CHANGE COMMON VARIABLES
               CALL ITEIG(ITER,COORDS,VECS,EVALMIN,EVALMAX,NS,SOVER,PTEST,VECL,CONVERGED)
               CEIG=CEIGSAVE; NEVS=NEVSSAVE; NEVL=NEVLSAVE ! RESET COMMON VARIABLES
               DIAG(1)=EVALMIN
            ELSE
               ABSTOL=DLAMCH('SAFE  MINIMUM')
               IF (ITER.GT.1) THEN
                  DO J1=1,NOPT
                     VECSP(J1)=VECS(J1)
                  ENDDO
               ENDIF
               CALL DSYEVR('V','I','U',NOPT,HESS,SIZE(HESS,1),0.0D0,1.0D0,1,NUSEEV,ABSTOL,NFOUND,DIAG,
     1                        ZWORK,3*NATOMS,ISUPPZ,WORK,
     2                        LWORK, IWORK, ILWORK, INFO )
               IF (INFO.NE.0) PRINT*,'WARNING - INFO=',INFO,' IN DSYEVR'
C              PRINT*,'OPTIMAL AND ACTUAL VALUES OF LWORK=',WORK(1),LWORK
C              PRINT*,'OPTIMAL AND ACTUAL VALUES OF ILWORK=',IWORK(1),ILWORK
               EVALMIN=DIAG(1)
               SOVER=0.0D0
               DO J1=1,NOPT
                  VECS(J1)=ZWORK(J1,1)
                  IF (ITER.GT.1) SOVER=SOVER+VECS(J1)*VECSP(J1)
               ENDDO
               IF (PTEST) THEN
                  IF (ITER.GT.1) THEN
                     WRITE(*,'(A,F15.7,A,F15.7)') ' SMALLEST EIGENVALUE=',EVALMIN,' OVERLAP WITH PREVIOUS VECTOR=',SOVER
                  ELSE
                     WRITE(*,'(A,F15.7,A,F15.7)') ' SMALLEST EIGENVALUE=',EVALMIN
                  ENDIF
               ENDIF
            ENDIF
         ELSE
            CEIGSAVE=CEIG; CEIG=HMCEIG; NEVSSAVE=NEVS; NEVS=HMNEVS ! CHANGE COMMON VARIABLES
            CALL BEIG(ITER,COORDS,ENERGY,VECS,EVALMIN,NS,SOVER,PTEST,CONVERGED)
            CEIG=CEIGSAVE; NEVS=NEVSSAVE  ! RESET COMMON VARIABLES
            DIAG(1)=EVALMIN
         ENDIF
      ENDIF
!
!  RETURN IF THE EIGENVALUE GOES ABOVE A CERTAIN THRESHOLD.
!
      IF (EVALMIN.GT.HMEVMAX) THEN
         FIXIMAGE=.FALSE.
         MFLAG=.FALSE.
         RETURN
      ENDIF

      CALL VECNORM(VECS,NOPT)
      DUMMY=0.0D0
      DO J1=1,NOPT
         ZWORK(J1,1)=VECS(J1)
         DUMMY=DUMMY+VECS(J1)**2
      ENDDO
!
!  SAVE XFOB FOR SCALING THE MAXIMUM ALLOWED STEP ALONG EACH EIGENVECTOR.
!
      DO I=1,NUSEEV
         XFOB(I)=0.0D0
         DO J = 1,NOPT
            XFOB(I)=XFOB(I)+GRAD(J)*ZWORK(J,I)
         ENDDO
      ENDDO
C
C  DUMP THE SMALLEST NON-ZERO EIGENVALUE AND EIGENVECTOR 
C  IN FILE VECTOR.DUMP, IF REQUIRED.
C
      IF (DUMPV) THEN
         IF (.NOT.ALLSTEPS) REWIND(44)
         WRITE(44,'(E20.10)') EVALMIN
         WRITE(44,'(3F20.10)') (ZWORK(J1,1),J1=1,NOPT)
         CALL FLUSH(44,ISTAT)
      ENDIF
C
C  TAKE AN EIGENVECTOR-FOLLOWING OR PAGE-MCIVER STEP DOWNHILL ALONG ALL KNOWN EIGENVECTORS.
C  THEN DO LBFGS MINIMIZATION IN THE TANGENT SPACE IF REQUIRED. 
C
      INEG=0
      DO J1=1,NUSEEV
         IF (DIAG(J1).LT.0.0D0) INEG=INEG+1
      ENDDO
      IF (DEBUG.AND.(INEG.GT.0)) WRITE(*,'(A,I5,A)') ' HYBRIDMIN> THERE ARE AT LEAST ',INEG,' NEGATIVE HESSIAN EIGENVALUES'
C
C  TAKE A STEP AWAY FROM A STATIONARY POINT ALONG THE APPROPRIATE
C  HESSIAN EIGENVECTOR. THIS ENABLES US TO START FROM CONVERGED TRANSITION STATES.
C
      AWAY=.FALSE.
      IF (RMS.LT.PUSHCUT) THEN
         IF ((INEG.NE.0).AND.CONVERGED) THEN ! DON;T TRY PUSHOFF IF CONVERGED IS .FALSE.
            IF ((ITER.EQ.1).AND.(INEG.EQ.1)) THEN
               IF (PTEST) THEN
                  IF (IVEC.GE.0) PRINT '(A)',' HYBRIDMIN> STEPPING AWAY FROM MINIMUM ALONG SOFTEST MODE + DIRECTION'
                  IF (IVEC.LT.0) PRINT '(A)',' HYBRIDMIN> STEPPING AWAY FROM MINIMUM ALONG SOFTEST MODE - DIRECTION'
               ENDIF
               AWAY=.TRUE.
            ELSE IF (MOD(ITER-1,4).EQ.0) THEN
               DO J1=1,NUSEEV
                  IF (DIAG(J1).LT.0.0D0) THEN
                     WRITE(*,'(A,I6)') ' HYBRIDMIN> STEPPING AWAY FROM SADDLE POINT ALONG EIGENVECTOR ',J1
                     IF (PUSHOFF.EQ.0.0D0) THEN
                        XFOB(J1)=STPMAX(J1)
                     ELSE
                        XFOB(J1)=PUSHOFF
                     ENDIF
                  ENDIF
               ENDDO
            ENDIF
         ENDIF
      ENDIF
      IF (HMMETHOD.EQ.'EF') THEN
C
C  EF DETERMINATION OF STEPS
C
         DO I=NUSEEV,1,-1
            STEP(I)=0.0D0
            LP1=DABS(DIAG(I))/2.0D0
            IF (MASST) THEN
               SUM=0
               DO J2=1,NATOMS
                  SUM=SUM+ATMASS(J2)
               ENDDO
               AVG=SUM/NATOMS
               LP2=1.0D0 + 4.0D0*((XFOB(I)/DIAG(I))**2)/AVG
            ELSE
               LP2=1.0D0 + 4.0D0*(XFOB(I)/DIAG(I))**2
            ENDIF
            LP=LP1*(1.0D0+DSQRT(LP2))
            IF (DEBUG) WRITE(*,'(A,I4,A,4X,F19.10)') ' HYBRIDMIN> MODE ',I,' WILL BE SEARCHED DOWNHILL. EIGENVALUE=',DIAG(I)
            STEP(I)=-XFOB(I)/LP
            IF (AWAY.AND.(I.EQ.1)) THEN
               IF (PUSHOFF.NE.0.0D0) THEN
                  STEP(I)=PUSHOFF
               ELSE
                  STEP(I)=STPMAX(1)/1.0D1
               ENDIF
               IF (IVEC.LT.0) STEP(I)=-STEP(I)
            ENDIF
         ENDDO
      ELSE IF (HMMETHOD.EQ.'PM') THEN
         IF (AWAY) THEN
            DO I=1,NUSEEV
               IF (PUSHOFF.NE.0.0D0) THEN
                  STEP(I)=PUSHOFF
               ELSE
                  STEP(I)=STPMAX(1)/1.0D1
               ENDIF
               IF (IVEC.LT.0) STEP(I)=-STEP(I)
            ENDDO
         ELSE
C
C  STEP AND SCALING DETERMINED BY PAGE-MCIVER SCHEME.
C  STPMAX(1) IS DYNAMICALLY ADJUSTED VIA A TRUST RADIUS SCHEME.
C     
            DELTAT=STPMAX(1)/(100.0D0*RMS*SQRT(1.0D0*NOPT))
            DELTAS=RMS*SQRT(1.0D0*NOPT)*DELTAT/2.0D0
            DO J1=1,NUSEEV
               DIAGEXP(J1)=DEXP(-2*DIAG(J1)*DELTAT)
            ENDDO
            J1=0

666         J1=J1+1
            DELTASP=DELTAS
            TEMP=0.0D0
            DO J2=1,NUSEEV
               TEMP=TEMP+XFOB(J2)**2*DIAGEXP(J2)**J1
            ENDDO 
            DELTAS=DELTAS+DSQRT(TEMP)*DELTAT
!
!  WE NEED TO ESCAPE FROM THE LOOP IF THE INTEGRAL HAS EFFECTIVELY CONVERGED
!  OR IF THE ARC LENGTH EXCEEDS THE ALLOWED VALUE. 
!  
            IF ((DELTAS.LT.STPMAX(1)).AND.(J1.LT.100000).AND.((DELTAS-DELTASP)/DELTASP.GT.1.0D-10)) GOTO 666

            TPAR=J1*DELTAT
            IF (PTEST) WRITE(*,'(A,G16.6,A,F15.3,A,I6)') ' EFOL> ESTIMATED ARC LENGTH=',DELTAS,' FOR T=',TPAR,
     &                               ' INTEGRATION STEPS=',J1

            STEP(1:NOPT)=0.0D0
            DO J1=1,NUSEEV
               STEP(J1)=XFOB(J1)*(DEXP(-DIAG(J1)*TPAR)-1)/DIAG(J1)
            ENDDO
         ENDIF
      ELSE 
         PRINT '(A,A)',' HYBRIDMIN> UNRECOGNISED HYBRID MINIMISATION STEP METHOD: ',HMMETHOD
         STOP
      ENDIF
C
C  SCALE ACCORDING TO STEP SIZE IN EV BASIS:
C
      STPMAG=MAX(DSQRT(DOTOPT(STEP(1),STEP(1),NUSEEV)),1.0D-10)
      IF (PTEST) WRITE(*,'(A,2F12.6)') ' HYBRIDMIN> % OF STEP AND GRADIENT ALONG SOFTEST MODE=',  
     &                              ABS(STEP(1))*100.0D0/STPMAG,ABS(XFOB(1))*100.0D0/(RMS*SQRT(1.0D0*NOPT))
      IF (.NOT.AWAY) THEN 
         DO J1=1,NUSEEV
            SCALE=MIN(STPMAX(J1)/MAX(DABS(STEP(J1)),1D-10),1.0D0)
            IF (DEBUG) PRINT '(A,I8,A,2G20.10)',' HYBRIDMIN> SCALED AND UNSCALED STEPS FOR MODE ',J1,' ARE: ',
     &                                            STEP(J1),SCALE*STEP(J1)
            STEP(J1)=SCALE*STEP(J1)
         ENDDO
      ENDIF
C
C  CONVERT THE STEPS TO THE CARTESIAN RATHER THAN THE EV BASIS AND PUT IN CSTEP(NOPT+1:2*NOPT)
C
      DO J=1,NOPT
         CSTEP(J)=0.0D0
         DO I=1,NUSEEV
            CSTEP(J)=CSTEP(J)+STEP(I)*ZWORK(J,I)
         ENDDO
      ENDDO
C
C  PUT NEW COORDINATES INTO COORDS.
C
      DO J=1,NOPT
         COORDS(J)=SAVECOORDS(J)+CSTEP(J)
      ENDDO
      KNOWE=.FALSE.
      KNOWG=.FALSE.
      KNOWH=.FALSE.

      ESAVE=ENERGY
      CALL POTENTIAL(COORDS,ENERGY,GRAD,.TRUE.,.FALSE.,RMS,PTEST,.FALSE.)
C
C  ADJUST MAXIMUM STEP SIZE.
C
      IF (ENERGY.GT.ESAVE+MAXERISE) THEN
         PRINT '(A,G20.10,A,G20.10,A)',' HYBRIDMIN> ENERGY INCREASED FROM ',ESAVE,' TO ',ENERGY,' - REVERSING STEP'
         DO J1=1,NOPT
            COORDS(J1)=COORDS(J1)-CSTEP(J1)
            STPMAX(J1)=MAX(STPMAX(J1)/2.0D0,MINMAX)
         ENDDO
         ENERGY=ESAVE
      ELSE
         DO J1=1,NUSEEV
            FOBNEW=0.0D0
            DO J2=1,NOPT
               FOBNEW=FOBNEW+GRAD(J2)*ZWORK(J2,J1)
            ENDDO
            XRAT(J1)=MIN(DABS(1.0D0-(FOBNEW-XFOB(J1))/(STEP(J1)*DIAG(J1))),DABS(1.0D0-(-FOBNEW-XFOB(J1))/(STEP(J1)*DIAG(J1))))
!           PRINT '(A,I8,5G15.5)','J1,XRAT, FOBNEW,XFOB,STEP,DIAG=',J1,XRAT(J1),FOBNEW,XFOB(J1),STEP(J1),DIAG(J1)
!           PRINT '(A,I8,5G15.5)','J1,XRAT,-FOBNEW,XFOB,STEP,DIAG=',J1,XRAT(J1),-FOBNEW,XFOB(J1),STEP(J1),DIAG(J1)
            IF (XRAT(J1).GT.TRAD) THEN
               STPMAX(J1)=MAX(STPMAX(J1)/1.1D0,MINMAX)
            ELSE
               STPMAX(J1)=MIN(STPMAX(J1)*1.1D0,MAXMAX)
            ENDIF
         ENDDO
      ENDIF
      CALL DUMPP(COORDS,ENERGY)
      CALL FLUSH(6,ISTAT)
C
C SUMMARIZE
C
      IF (PTEST) THEN
         WRITE(*,30)
30       FORMAT(1X,79('-'))
         WRITE(*,40)
40       FORMAT(' VECTOR      GRADIENT        SECDER       STEP          MAX STEP    TRUST RATIO')
         WRITE(*,30)
         DO I=NUSEEV,1,-1
            WRITE(*,50) I,XFOB(I),DIAG(I),STEP(I),STPMAX(I),XRAT(I)
         ENDDO
50       FORMAT(1X,I4,1X,E15.6,1X,E15.6,1X,E13.6,1X,E12.6,1X,E15.6)
         WRITE(*,30)
      ENDIF
C
C  TANGENT SPACE MINIMIZATION NEXT.
C  UPHILL DIRECTION IS PROJECTED OUT OF THE STEP IN MYLBFGS
C  THE NEXT IF BLOCK ALLOWS FOR ZERO TANGENT SPACE STEPS IN THE INITIAL PHASE
C
      IF ((HMNBFGSMAX1.EQ.0).AND.((1.0D0-DABS(SOVER).GT.0.0001D0))) THEN
         FIXIMAGE=.FALSE.
         ITER=ITER+1
         MFLAG=.FALSE.
         IF (ITER.GT.ITMAX) RETURN
         GOTO 90
      ENDIF

      IF (((1.0D0-DABS(SOVER).GT.0.0001D0).OR.(INEG.NE.0)).OR.(ITER.EQ.1)) THEN
          NBFGS=HMNBFGSMAX1
      ELSE
          NBFGS=HMNBFGSMAX2
      ENDIF

      RESET=.FALSE.
      IF (ITER.EQ.1) RESET=.TRUE.
      IF (CHRMMT.AND.INTMINT) THEN
         CALL MYLBFGS(NINTS,MUPDATE,COORDS,.FALSE.,MFLAG,ENERGY,RMS2,EREAL,RMS,NBFGS,
     1                RESET,ITDONE,PTEST,GRAD,.TRUE.,.TRUE.)
      ELSE
         CALL MYLBFGS(NOPT,MUPDATE,COORDS,.FALSE.,MFLAG,ENERGY,RMS2,EREAL,RMS,NBFGS,
     1                RESET,ITDONE,PTEST,GRAD,.TRUE.,.TRUE.)
      ENDIF
      RMS2=RMS ! SAVE SUBSPACE RMS
      RMS=DSQRT(DOTOPT(GRAD(1),GRAD(1),NOPT))/SQRT(1.0D0*NOPT) ! TRUE RMS
      IF (PTEST) WRITE(*,'(A,F15.7,A,F15.7,A,F15.7)') ' HYBRIDMIN> TOTAL RMS GRADIENT=',RMS,
     &         ' SUBSPACE GRADIENT=',RMS2,' UNSCALED SECOND ORDER STEP LENGTH=',STPMAG
      IF (MFLAG) THEN
         IF (((RMS.GT.CONVR).OR.(INEG.GT.0)).OR.(STPMAG.GT.CONVU)) MFLAG=.FALSE.
         IF (MFLAG) RETURN
      ENDIF 

      ITER=ITER+1
      IF (ITER.GT.ITMAX) RETURN
      GOTO 90

      RETURN
      END
