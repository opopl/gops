!   OPTIM: A PROGRAM FOR OPTIMIZING GEOMETRIES AND CALCULATING REACTION PATHWAYS
!   COPYRIGHT (C) 1999-2006 DAVID J. WALES
!   THIS FILE IS PART OF OPTIM.
!   
!   OPTIM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
!   IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
!   THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
!   (AT YOUR OPTION) ANY LATER VERSION.
!   
!   OPTIM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL,
!   BUT WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
!   MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE.  SEE THE
!   GNU GENERAL PUBLIC LICENSE FOR MORE DETAILS.
!   
!   YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
!   ALONG WITH THIS PROGRAM; IF NOT, WRITE TO THE FREE SOFTWARE
!   FOUNDATION, INC., 59 TEMPLE PLACE, SUITE 330, BOSTON, MA  02111-1307  USA
!
SUBROUTINE GUESSPATH(START,FINISH,NCOORDS,EDIFFTOLLOCAL,NATOMS)
USE KEY
USE MODCHARMM
USE MODGUESS
USE MODUNRES
USE PORFUNCS
IMPLICIT NONE
INTEGER, INTENT(IN) :: NCOORDS, NATOMS
DOUBLE PRECISION, INTENT(IN) :: START(NCOORDS), FINISH(NCOORDS), EDIFFTOLLOCAL
INTEGER, ALLOCATABLE :: STEPORDER(:)
DOUBLE PRECISION, ALLOCATABLE :: INTERPENERGY(:), INTERPENERGYSAVE(:), PEPCOORDS(:), SMALLSTEP(:), LARGESTEP(:), DELTA(:)
DOUBLE PRECISION :: MAXE, SUME, NEWMAXE
INTEGER :: JMAXE(1), NEWJMAXE(1)
INTEGER :: NDUMMY, ISTAT
LOGICAL :: IMPROVED
LOGICAL, ALLOCATABLE :: SHORTER(:)
DOUBLE PRECISION, PARAMETER :: PI=3.141592653589793D0
CHARACTER(LEN=80) LFNAME

LOGICAL :: SEQUENTIAL(NCOORDS), DOSEQ(NCOORDS)
INTEGER :: NSEQ, J1, NCOUNT

IF (CHRMMT) THEN
   WRITE(*,'(A)') 'FOR CHARMM THERE MAY BE MORE INTERNAL COORDINATES THAN CARTESIANS AND THE DIMENSIONS NEED ATTENTION'
   STOP
ENDIF
IF (UNRST) ALLOCATE(PEPCOORDS(3*NATOMS),SMALLSTEP(NCOORDS),LARGESTEP(NCOORDS),DELTA(NCOORDS))
ALLOCATE(SHORTER(NCOORDS))
! FIRST DETERMINE THE NUMBER OF COORDINATES TO BE TREATED SEQUENTIALLY ACCORDING TO GTHRESHOLD
NSEQ=0
! DEFINE THE ANGULAR STEP FOR THE SHORTEST AND LONGEST ANGULAR ROUTES BETWEEN START AND
! FINISH FOR UNRES.
IF (UNRST) THEN
   DO J1=1,NCOORDS
      IF (ABS(FINISH(J1)-START(J1)).LE.PI) THEN
         SMALLSTEP(J1)=FINISH(J1)-START(J1)
         IF (FINISH(J1).GT.START(J1)) THEN
            LARGESTEP(J1)=FINISH(J1)-START(J1)-2*PI
         ELSE
            LARGESTEP(J1)=FINISH(J1)-START(J1)+2*PI
         ENDIF
      ELSE
         LARGESTEP(J1)=FINISH(J1)-START(J1)
         IF (FINISH(J1).GT.START(J1)) THEN
            SMALLSTEP(J1)=FINISH(J1)-START(J1)-2*PI
         ELSE
            SMALLSTEP(J1)=FINISH(J1)-START(J1)+2*PI
         ENDIF
      ENDIF
      SEQUENTIAL(J1)=.FALSE.
      DOSEQ(J1)=.FALSE.
      IF (ABS(SMALLSTEP(J1)).GT.GTHRESHOLD) THEN
         SEQUENTIAL(J1)=.TRUE.
         DOSEQ(J1)=.TRUE.
         NSEQ=NSEQ+1
      ENDIF
!     WRITE(*,'(A,2I5,2G20.10,4X,L1)') 'J1,NSEQ,SMALLSTEP(J1),LARGESTEP(J1)=',J1,NSEQ,SMALLSTEP(J1),LARGESTEP(J1),SEQUENTIAL(J1)
      DELTA(J1)=SMALLSTEP(J1)
      SHORTER(J1)=.TRUE.
   ENDDO
ELSE
   DO J1=1,NCOORDS
      SEQUENTIAL(J1)=.FALSE.
      DOSEQ(J1)=.FALSE.
      IF (ABS(START(J1)-FINISH(J1)).GT.GTHRESHOLD) THEN
         SEQUENTIAL(J1)=.TRUE.
         DOSEQ(J1)=.TRUE.
         NSEQ=NSEQ+1
         WRITE(*,'(A,2I5,2G20.10)') 'J1,NSEQ,START,FINISH=',J1,NSEQ,START(J1),FINISH(J1)
      ENDIF
   ENDDO
ENDIF
IF (NSEQ.EQ.0) THEN
   WRITE(*,'(A,F12.3)') ' NO SEQUENTIAL COORDINATES FOR A THRESHOLD OF ',GTHRESHOLD
   NINTERP=0
   CALL FLUSH(6,ISTAT)
   RETURN
ENDIF
WRITE(*,'(2(A,I6))') ' NUMBER OF COORDINATES TO BE TREATED SEQUENTIALLY IN GUESSPATH=',NSEQ, &
                     ' OUT OF ', NCOORDS
NINTERP=NSEQ*GSTEPS-1
ALLOCATE(STEPORDER(NSEQ*GSTEPS),INTERPENERGY(NSEQ*GSTEPS),INTERPENERGYSAVE(NSEQ*GSTEPS))
NCOUNT=0
! INITIAL ORDER OF STEPS FOR SEQUENTIAL COORDINATES
DO J1=1,NCOORDS
   IF (SEQUENTIAL(J1)) THEN
      NCOUNT=NCOUNT+1
      STEPORDER(GSTEPS*(NCOUNT-1)+1:GSTEPS*NCOUNT)=J1
   ENDIF
ENDDO
! TO OPTIMISE THE ORDER OF THE STEPS WE NEED TO RECALCULATE INTERPENERGY FOR ALL CONFIGURATIONS
! BETWEEN THE ELEMENTS OF STEPORDER THAT HAVE BEEN CHANGED, AND SAVE THE CURRENT OPTIMAL VALUES
! OF THE HIGHEST CONFIGURATION AND SUM OF ENERGIES. 
! 
! THE ENERGY AT STEP NSEQ*GSTEPS IS JUST THE ENERGY OF THE MINIMUM IN FINISH - SHOULD NEVER CHANGE

CALL GENERGIES(1,NSEQ*GSTEPS,.FALSE.) ! FILL THE INITIAL INTERPENERGY VECTOR WITH INTERPOLATED ENERGIES
INTERPENERGYSAVE(1:NSEQ*GSTEPS)=INTERPENERGY(1:NSEQ*GSTEPS)

MAXE=MAXVAL(INTERPENERGY(1:NSEQ*GSTEPS-1))
JMAXE=MAXLOC(INTERPENERGY(1:NSEQ*GSTEPS-1)) ! NOTE THAT MAXLOC RETURNS A VECTOR OF DIMENSION ONE, NOT A SCALAR
SUME=SUM(INTERPENERGY(1:NSEQ*GSTEPS))

WRITE(*,'(A,2G20.10)') 'MAXIMUM ENERGY AND  MEAN=',MAXE,SUME/(NSEQ*GSTEPS-1)
WRITE(*,'(A,I6)') 'MAXIMUM IS AT LOCATION ',JMAXE(1)
PRINT*,'ORDER OF COORDINATE STEPS, ENERGIES:'
WRITE(*,'(2I6,G20.10)') (J1,STEPORDER(J1),INTERPENERGY(J1),J1=1,NSEQ*GSTEPS)
! TRY CHANGING THE STEP IN POSITION JMAXE(1) WITH ALL THE OTHERS IN TURN TO FIND THE LOWEST
! MAXE THAT RESULTS
DO 
   IMPROVED=.FALSE.
! FOR UNRES FIRST TRY USING LARGESTEP INSTEAD OF SMALLSTEP FOR THE DEGREE OF FREEDOM
! CORRESPONDING TO JMAXE(1). OR TRY SMALLSTEP INSTEAD OF LARGESTEP IF WE'VE CHANGED THIS
! AN ODD NUMBER OF TIMES ALREADY!
   IF (UNRST) THEN
      IF (SHORTER(STEPORDER(JMAXE(1)))) THEN
         WRITE(*,'(A,I5)') ' TRYING LONGER INTERPOLATION FOR COORDINATE ',STEPORDER(JMAXE(1))
         DELTA(STEPORDER(JMAXE(1)))=LARGESTEP(STEPORDER(JMAXE(1)))
         SHORTER(STEPORDER(JMAXE(1)))=.FALSE.
      ELSE
         WRITE(*,'(A,I5)') ' TRYING SHORTER INTERPOLATION FOR COORDINATE ',STEPORDER(JMAXE(1))
         DELTA(STEPORDER(JMAXE(1)))=SMALLSTEP(STEPORDER(JMAXE(1)))
         SHORTER(STEPORDER(JMAXE(1)))=.TRUE.
      ENDIF
      CALL GENERGIES(1,NSEQ*GSTEPS-1,.FALSE.) 
      NEWMAXE=MAXVAL(INTERPENERGY(1:NSEQ*GSTEPS-1))
      NEWJMAXE=MAXLOC(INTERPENERGY(1:NSEQ*GSTEPS-1))
      WRITE(*,'(A,G20.10,A,I6)') ' MAXIMUM ENERGY IS NOW ',NEWMAXE,' AT POSITION ',NEWJMAXE(1)
      IF (MAXE-NEWMAXE.GT.EDIFFTOLLOCAL) THEN ! ACCEPT CHANGE
         MAXE=NEWMAXE
         INTERPENERGYSAVE(1:NSEQ*GSTEPS-1)=INTERPENERGY(1:NSEQ*GSTEPS-1)
         IMPROVED=.TRUE.
         IF (JMAXE(1).NE.NEWJMAXE(1)) THEN
            JMAXE(1)=NEWJMAXE(1)
            CYCLE
         ENDIF
      ELSE
         INTERPENERGY(1:NSEQ*GSTEPS-1)=INTERPENERGYSAVE(1:NSEQ*GSTEPS-1)
         IF (SHORTER(STEPORDER(JMAXE(1)))) THEN
            DELTA(STEPORDER(JMAXE(1)))=LARGESTEP(STEPORDER(JMAXE(1)))
            SHORTER(STEPORDER(JMAXE(1)))=.FALSE.
         ELSE
            DELTA(STEPORDER(JMAXE(1)))=SMALLSTEP(STEPORDER(JMAXE(1)))
            SHORTER(STEPORDER(JMAXE(1)))=.TRUE.
         ENDIF
      ENDIF

      DO J1=1,NCOORDS
         IF (SEQUENTIAL(J1).AND.(J1.NE.STEPORDER(JMAXE(1)))) THEN
            IF (SHORTER(J1)) THEN
               WRITE(*,'(A,I5)') ' TRYING LONGER INTERPOLATION FOR COORDINATE ',J1
               DELTA(J1)=LARGESTEP(J1)
               SHORTER(J1)=.FALSE.
            ELSE
               WRITE(*,'(A,I5)') ' TRYING SHORTER INTERPOLATION FOR COORDINATE ',J1
               DELTA(J1)=SMALLSTEP(J1)
               SHORTER(J1)=.TRUE.
            ENDIF
            CALL GENERGIES(1,NSEQ*GSTEPS-1,.FALSE.) 
            NEWMAXE=MAXVAL(INTERPENERGY(1:NSEQ*GSTEPS-1))
            NEWJMAXE=MAXLOC(INTERPENERGY(1:NSEQ*GSTEPS-1))
            WRITE(*,'(A,G20.10,A,I6)') ' MAXIMUM ENERGY IS NOW ',NEWMAXE,' AT POSITION ',NEWJMAXE(1)
            IF (MAXE-NEWMAXE.GT.EDIFFTOLLOCAL) THEN ! ACCEPT CHANGE
               MAXE=NEWMAXE
               INTERPENERGYSAVE(1:NSEQ*GSTEPS-1)=INTERPENERGY(1:NSEQ*GSTEPS-1)
               IMPROVED=.TRUE.
               IF (JMAXE(1).NE.NEWJMAXE(1)) THEN
                  JMAXE(1)=NEWJMAXE(1)
                  CYCLE
               ENDIF
            ELSE
               INTERPENERGY(1:NSEQ*GSTEPS-1)=INTERPENERGYSAVE(1:NSEQ*GSTEPS-1)
               IF (SHORTER(J1)) THEN
                  DELTA(J1)=LARGESTEP(J1)
                  SHORTER(J1)=.FALSE.
               ELSE
                  DELTA(J1)=SMALLSTEP(J1)
                  SHORTER(J1)=.TRUE.
               ENDIF
            ENDIF
         ENDIF
      ENDDO
   ENDIF

   WRITE(*,'(A,I6,A,G20.10,A,L3,A,G20.10)') ' TRY TOGGLING LINEAR/SEQUENTIAL INTERPOLATION FOR ELIGIBLE COORDINATES'
   DO J1=1,NCOORDS
      IF (SEQUENTIAL(J1)) THEN
         IF (DOSEQ(J1)) THEN
            WRITE(*,'(A,I5)') ' TRYING LINEAR INTERPOLATION FOR COORDINATE ',J1
            DOSEQ(J1)=.FALSE.
         ELSE
            WRITE(*,'(A,I5)') ' TRYING SEQUENTIAL INTERPOLATION FOR COORDINATE ',J1
            DOSEQ(J1)=.TRUE.
         ENDIF
         CALL GENERGIES(1,NSEQ*GSTEPS-1,.FALSE.)
         NEWMAXE=MAXVAL(INTERPENERGY(1:NSEQ*GSTEPS-1))
         NEWJMAXE=MAXLOC(INTERPENERGY(1:NSEQ*GSTEPS-1))
         WRITE(*,'(A,G20.10,A,I6)') ' MAXIMUM ENERGY IS NOW ',NEWMAXE,' AT POSITION ',NEWJMAXE(1)
         IF (MAXE-NEWMAXE.GT.EDIFFTOLLOCAL) THEN ! ACCEPT CHANGE
            PRINT*,'ACCEPTING LINEAR/SEQUENTIAL CHANGE'
            MAXE=NEWMAXE
            INTERPENERGYSAVE(1:NSEQ*GSTEPS-1)=INTERPENERGY(1:NSEQ*GSTEPS-1)
            IMPROVED=.TRUE.
            IF (JMAXE(1).NE.NEWJMAXE(1)) THEN
               JMAXE(1)=NEWJMAXE(1)
               CYCLE
            ENDIF
         ELSE
            PRINT*,'REJECTING LINEAR/SEQUENTIAL CHANGE'
            INTERPENERGY(1:NSEQ*GSTEPS-1)=INTERPENERGYSAVE(1:NSEQ*GSTEPS-1)
            IF (DOSEQ(J1)) THEN
               DOSEQ(J1)=.FALSE.
            ELSE
               DOSEQ(J1)=.TRUE.
            ENDIF
         ENDIF
      ENDIF
   ENDDO

   IF (UNRST) THEN
      WRITE(*,'(A,I6,A,G20.10,A,L3,A,G20.10)') ' STARTING SERIES OF EXCHANGES FOR STEP IN POSITION ',JMAXE(1), &
                              ' HIGHEST ENERGY=',MAXE,' SHORTER=',SHORTER(STEPORDER(JMAXE(1))),' STEP=',DELTA(STEPORDER(JMAXE(1)))
   ELSE
      WRITE(*,'(A,I6,A,G20.10,A,L3,A,G20.10)') ' STARTING SERIES OF EXCHANGES FOR STEP IN POSITION ',JMAXE(1), &
                              ' HIGHEST ENERGY=',MAXE
   ENDIF
   DO J1=1,NSEQ*GSTEPS
      IF (J1.EQ.JMAXE(1)) CYCLE
      NDUMMY=STEPORDER(J1)
      STEPORDER(J1)=STEPORDER(JMAXE(1))
      STEPORDER(JMAXE(1))=NDUMMY
      CALL GENERGIES(J1,JMAXE(1),.FALSE.) ! FILL THE INITIAL INTERPENERGY VECTOR WITH INTERPOLATED ENERGIES
      NEWMAXE=MAXVAL(INTERPENERGY(1:NSEQ*GSTEPS-1))
      NEWJMAXE=MAXLOC(INTERPENERGY(1:NSEQ*GSTEPS-1))
      WRITE(*,'(2(A,I6),A,G20.10,2(A,I6))') ' AFTER SWAPPING STEPS AT LOCATIONS ',J1,' AND ',JMAXE(1), &
                            ' MAXIMUM ENERGY=',NEWMAXE,' AT POSITION ',NEWJMAXE(1),' COORDINATE ',STEPORDER(NEWJMAXE(1))
      IF (MAXE-NEWMAXE.GT.EDIFFTOLLOCAL) THEN ! ACCEPT SWAP
         MAXE=NEWMAXE
         INTERPENERGYSAVE(1:NSEQ*GSTEPS-1)=INTERPENERGY(1:NSEQ*GSTEPS-1)
         IMPROVED=.TRUE.
         IF (JMAXE(1).NE.NEWJMAXE(1)) THEN
            JMAXE(1)=NEWJMAXE(1)
            EXIT
         ENDIF
      ELSE ! UNDO SWAP
         INTERPENERGY(1:NSEQ*GSTEPS-1)=INTERPENERGYSAVE(1:NSEQ*GSTEPS-1)
         NDUMMY=STEPORDER(J1)
         STEPORDER(J1)=STEPORDER(JMAXE(1))
         STEPORDER(JMAXE(1))=NDUMMY
      ENDIF
   ENDDO
   IF (.NOT.IMPROVED) EXIT
ENDDO 

SUME=SUM(INTERPENERGY(1:NSEQ*GSTEPS-1))
WRITE(*,'(A,2G20.10)') 'MAXIMUM ENERGY AND MEAN=',MAXE,SUME/(NSEQ*GSTEPS-1)
WRITE(*,'(A,I6)') 'MAXIMUM IS AT LOCATION ',JMAXE(1)
PRINT*,'ORDER OF COORDINATE STEPS, ENERGIES:'
WRITE(*,'(2I6,G20.10)') (J1,STEPORDER(J1),INTERPENERGY(J1),J1=1,NSEQ*GSTEPS)
! DUMP XYZ PATH
CALL GENERGIES(1,NSEQ*GSTEPS,.TRUE.) 

DEALLOCATE(STEPORDER,INTERPENERGY,INTERPENERGYSAVE,SHORTER)
IF (ALLOCATED(PEPCOORDS)) DEALLOCATE(PEPCOORDS,DELTA,SMALLSTEP,LARGESTEP)
CALL FLUSH(6,ISTAT)

CONTAINS
   SUBROUTINE GENERGIES(BOTTOM,TOP,DUMPPATH)
   INTEGER, INTENT(IN) :: BOTTOM, TOP
   INTEGER :: K1, K2, NDUMMY, LBOTTOM, LTOP, K3
   LOGICAL, INTENT(IN) :: DUMPPATH
   DOUBLE PRECISION :: X(3*NATOMS), RMS, GRAD(3*NATOMS)
               ! THERE SHOULD BE A WAY OF NOT 
               ! DECLARING THE HESSIAN UNLESS IT IS ACTUALLY NEEDED? USE (*,*) IN POTENTIAL
               ! AND ROUTINES CALLED FROM IT?
   DOUBLE PRECISION :: DPRAND, ENERGY


   IF (DUMPPATH) THEN
      IF (FILTH.EQ.0) THEN
         OPEN(UNIT=7,FILE='GUESS.XYZ',STATUS='UNKNOWN')
      ELSE
         LFNAME='GUESS.XYZ.'//TRIM(ADJUSTL(FILTHSTR))
         OPEN(UNIT=7,FILE=TRIM(ADJUSTL(LFNAME)),STATUS='UNKNOWN')
      ENDIF
      IF (UNRST) THEN
         IF (FILTH.EQ.0) THEN
            OPEN(UNIT=8,FILE='GUESS.UNRES.XYZ',STATUS='UNKNOWN')
         ELSE
            LFNAME='GUESS.UNRES.XYZ.'//TRIM(ADJUSTL(FILTHSTR))
            OPEN(UNIT=8,FILE=TRIM(ADJUSTL(LFNAME)),STATUS='UNKNOWN')
         ENDIF
!CALL VAR_TO_GEOM(NCOORDS,START)
!CALL CHAINBUILD
         DO K2=1,NRES
            WRITE(7,'(3G20.10)') C(1,K2),C(2,K2),C(3,K2) ! BACKBONE
            WRITE(7,'(3G20.10)') C(1,K2+NRES),C(2,K2+NRES),C(3,K2+NRES) ! SIDE CHAINS
         ENDDO
         DO K2=1,(NATOMS/2)-1 ! JMC ADD PEPTIDE ATOMS...
            DO K3=1,3
               PEPCOORDS(6*(K2-1)+K3)=(2.0D0*C(K3,K2)+C(K3,K2+1))/3.0D0
               PEPCOORDS(6*(K2-1)+K3+3)=(C(K3,K2)+2.0D0*C(K3,K2+1))/3.0D0
            ENDDO
         ENDDO
         WRITE(8,'(I6)') 2*NATOMS-2
         WRITE(8,'(A)') ' '
         WRITE(8,'(A2,3G20.10)') ('C  ',C(1,K2),C(2,K2),C(3,K2),K2=1,NATOMS)
         WRITE(8,'(A2,4X,3F20.10)') ('O ',PEPCOORDS(6*(K2-1)+1),PEPCOORDS(6*(K2-1)+2),PEPCOORDS(6*(K2-1)+3) &
                                   ,K2=1,(NATOMS/2)-1)
         WRITE(8,'(A2,4X,3F20.10)') ('N ',PEPCOORDS(6*(K2-1)+4),PEPCOORDS(6*(K2-1)+5),PEPCOORDS(6*(K2-1)+6) &
                                   ,K2=1,(NATOMS/2)-1)
      ELSE
         WRITE(7,'(I6)') NATOMS
         WRITE(7,'(A)') ' '
         WRITE(7,'(A2,3G20.10)') ('LA  ',START(3*K2-2),START(3*K2-1),START(3*K2),K2=1,NCOORDS/3)
      ENDIF
   ENDIF
   LBOTTOM=BOTTOM
   LTOP=TOP
   IF (LBOTTOM.GT.LTOP) THEN
      NDUMMY=LBOTTOM
      LBOTTOM=LTOP
      LTOP=NDUMMY
   ENDIF
   X(1:NCOORDS)=START(1:NCOORDS)
! DO THE FIRST LBOTTOM STEPS - THE ORDER ONLY CHANGES BETWEEN LBOTTOM AND LTOP
   IF (UNRST) THEN
      DO K1=1,LBOTTOM-1
         X(STEPORDER(K1))=X(STEPORDER(K1))+DELTA(STEPORDER(K1))/GSTEPS
      ENDDO
   ELSE
      DO K1=1,LBOTTOM-1
         X(STEPORDER(K1))=X(STEPORDER(K1))+(FINISH(STEPORDER(K1))-START(STEPORDER(K1)))/GSTEPS
      ENDDO
   ENDIF
! REMOVE EXACT ATOM SUPERPOSITIONS USING SOME RANDOM NUMERICAL NOISE WITH DPRAND
   DO K1=LBOTTOM,LTOP
      IF (DOSEQ(STEPORDER(K1))) THEN
         IF (UNRST) THEN
            X(STEPORDER(K1))=X(STEPORDER(K1))+DELTA(STEPORDER(K1))/GSTEPS
         ELSE
            X(STEPORDER(K1))=X(STEPORDER(K1))+ &
               (FINISH(STEPORDER(K1))-START(STEPORDER(K1)))/GSTEPS+(DPRAND()-1.0D0)*1.0D-20
         ENDIF
      ENDIF
      DO K2=1,NCOORDS ! LINEAR INTERPOLATION
         IF (.NOT.SEQUENTIAL(K2).OR.(.NOT.DOSEQ(K2))) THEN
            IF (UNRST) THEN
               X(K2)=START(K2)+DELTA(K2)*K1/(NSEQ*GSTEPS)
            ELSE
               X(K2)=(START(K2)*(NSEQ*GSTEPS-K1)+FINISH(K2)*K1)/(NSEQ*GSTEPS) +(DPRAND()-1.0D0)*1.0D-20
            ENDIF
         ENDIF
      ENDDO
      IF (UNRST) THEN
!CALL VAR_TO_GEOM(NCOORDS,X)
!CALL CHAINBUILD
      ENDIF
      IF (DUMPPATH) THEN
         IF (UNRST) THEN
            DO K2=1,NRES
               WRITE(7,'(3G20.10)') C(1,K2),C(2,K2),C(3,K2) ! BACKBONE
               WRITE(7,'(3G20.10)') C(1,K2+NRES),C(2,K2+NRES),C(3,K2+NRES) ! SIDE CHAINS
            ENDDO
            DO K2=1,(NATOMS/2)-1 ! JMC ADD PEPTIDE ATOMS...
               DO K3=1,3
                  PEPCOORDS(6*(K2-1)+K3)=(2.0D0*C(K3,K2)+C(K3,K2+1))/3.0D0
                  PEPCOORDS(6*(K2-1)+K3+3)=(C(K3,K2)+2.0D0*C(K3,K2+1))/3.0D0
               ENDDO
            ENDDO
            WRITE(8,'(I6)') 2*NATOMS-2
            WRITE(8,'(A)') ' '
            WRITE(8,'(A2,3G20.10)') ('C  ',C(1,K2),C(2,K2),C(3,K2),K2=1,NATOMS)
            WRITE(8,'(A2,4X,3F20.10)') ('O ',PEPCOORDS(6*(K2-1)+1),PEPCOORDS(6*(K2-1)+2),PEPCOORDS(6*(K2-1)+3) &
                                      ,K2=1,(NATOMS/2)-1)
            WRITE(8,'(A2,4X,3F20.10)') ('N ',PEPCOORDS(6*(K2-1)+4),PEPCOORDS(6*(K2-1)+5),PEPCOORDS(6*(K2-1)+6) &
                                      ,K2=1,(NATOMS/2)-1)
         ELSE
            WRITE(7,'(I6)') NATOMS
            WRITE(7,'(A)') ' '
            WRITE(7,'(A2,3G20.10)') ('LA  ',X(3*K2-2),X(3*K2-1),X(3*K2),K2=1,NCOORDS/3)
         ENDIF
      ELSE
         CALL POTENTIAL(X,ENERGY,GRAD,.FALSE.,.FALSE.,RMS,.FALSE.,.FALSE.)
         INTERPENERGY(K1)=ENERGY
      ENDIF
   ENDDO
   IF (DUMPPATH) CLOSE(7)
   IF (DUMPPATH.AND.UNRST) CLOSE(8)

   END SUBROUTINE GENERGIES

END SUBROUTINE GUESSPATH
