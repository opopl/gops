!     COPYRIGHT (C) 1999-2008 DAVID J. WALES
!  THIS FILE IS PART OF OPTIM.
!
!  OPTIM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
!  IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
!  THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
!  (AT YOUR OPTION) ANY LATER VERSION.
!
!  OPTIM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL,
!  BUT WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
!  MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE.  SEE THE
!  GNU GENERAL PUBLIC LICENSE FOR MORE DETAILS.
!
!  YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
!  ALONG WITH THIS PROGRAM; IF NOT, WRITE TO THE FREE SOFTWARE
!  FOUNDATION, INC., 59 TEMPLE PLACE, SUITE 330, BOSTON, MA  02111-1307  USA
!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!
SUBROUTINE MINPERMRB(COORDSB,COORDSA,NATOMS,DEBUG,BOXLX,BOXLY,BOXLZ,BULKT,TWOD,DISTANCE,DIST2,RIGID,RMATBEST)
USE KEY,ONLY : NPERMGROUP, NPERMSIZE, PERMGROUP, NSETS, SETS, STOCKT, GEOMDIFFTOL, AMBERT, &
  &            NFREEZE, NABT, RBAAT, ANGLEAXIS2, LPDGEOMDIFFTOL, BESTPERM, RBCUTOFF, NRBGROUP, RBGROUP, RBNINGROUP, &
  &            RBATOMSMAX
USE MODCHARMM,ONLY : CHRMMT
USE INTCOMMONS, ONLY : INTMINPERMT, INTINTERPT, DESMINT
USE COMMONS, ONLY : ZSYM
USE INTCUTILS, ONLY : INTMINPERM
IMPLICIT NONE

INTEGER NATOMS, NPERM, PATOMS, NCOUNT, NRUNNING1, NRUNNING2
INTEGER J3, PERM(NATOMS),NDUMMY, LPERM(NATOMS), J1, J2, NRUNNING
DOUBLE PRECISION DIST2, COORDSA(3*NATOMS), COORDSB(3*NATOMS), DISTANCE, DUMMYA(3*NATOMS), DUMMYB(3*NATOMS), DUMMY(3*NATOMS)
DOUBLE PRECISION BOXLX,BOXLY,BOXLZ,WORSTRAD,RMAT(3,3),ENERGY, VNEW(3*NATOMS), DX, DY, DZ, RMS, DBEST, XBEST(3*NATOMS)
DOUBLE PRECISION CMXA, CMXB, CMXC
DOUBLE PRECISION ROTA(3,3), ROTINVA(3,3), ROTB(3,3), ROTINVB(3,3), ROTINVBBEST(3,3), ROTABEST(3,3), RMATBEST(3,3), TMAT(3,3)
DOUBLE PRECISION CMAX, CMAY, CMAZ, CMBX, CMBY, CMBZ, RMATCUMUL(3,3)
LOGICAL DEBUG, TWOD, RIGID, BULKT, DONEGROUP(NATOMS), RBASSIGNED(NATOMS), TRYMERGE
DOUBLE PRECISION PDUMMYA(3*NATOMS), PDUMMYB(3*NATOMS), LDISTANCE, DUMMYC(3*NATOMS), XDUMMY, DISTA(NATOMS), DISTB(NATOMS), LDUMMY
DOUBLE PRECISION SAVEDIST(NATOMS), PDUMMYASAVE(3*NATOMS)

INTEGER SAVENPERMGROUP, SAVENPERMSIZE(NATOMS), SAVEPERMGROUP(NATOMS), SAVENSETS(NATOMS)
INTEGER SAVENATOMS, ATOMMAP(NATOMS), INVMAP(NATOMS), NNDUMMY, NNNDUMMY, J4, J5, NEXTRA, NSAVE, NPERMTOTAL, TEMPRBNINGROUP(NATOMS)
INTEGER, ALLOCATABLE :: RBTEMP(:), TEMPRBGROUP(:)

RBASSIGNED(1:NATOMS)=.FALSE.
NRBGROUP=0
SAVENPERMGROUP=NPERMGROUP
SAVENPERMSIZE(1:NATOMS)=NPERMSIZE(1:NATOMS)
SAVEPERMGROUP(1:NATOMS)=PERMGROUP(1:NATOMS)
SAVENSETS(1:NATOMS)=NSETS(1:NATOMS)
SAVENATOMS=NATOMS
DONEGROUP(1:NATOMS)=.FALSE.

! OPEN(UNIT=876,FILE='COORDSA.XYZ',STATUS='UNKNOWN')
! WRITE(876,'(I4)') SAVENATOMS
! WRITE(876,'(A)') 'COORDSA INITIALLY'
! WRITE(876,'(A1,1X,3F20.10)') (ZSYM((J1+2)/3),COORDSA(J1),COORDSA(J1+1),COORDSA(J1+2),J1=1,3*NATOMS,3)
! OPEN(UNIT=774,FILE='COORDSB.XYZ',STATUS='UNKNOWN')
! WRITE(774,'(I4)') SAVENATOMS
! WRITE(774,'(A)') 'COORDSB INITIALLY'
! WRITE(774,'(A1,1X,3F20.10)') (ZSYM((J1+2)/3),COORDSB(J1),COORDSB(J1+1),COORDSB(J1+2),J1=1,3*NATOMS,3)

! CALL POTENTIAL(COORDSA,ENERGY,VNEW,.TRUE.,.FALSE.,RMS,.FALSE.,.FALSE.)
! PRINT '(A,2F20.10)',' MINPERMRB> AT THE START FOR MIN A ENERGY AND RMS ARE: ',ENERGY, RMS
! CALL POTENTIAL(COORDSB,ENERGY,VNEW,.TRUE.,.FALSE.,RMS,.FALSE.,.FALSE.)
! PRINT '(A,2F20.10)',' MINPERMRB> AT THE START FOR MIN B ENERGY AND RMS ARE: ',ENERGY, RMS

!
! TRYING TO MERGE GROUPS WITH COMMON ATOMS MAY FIX SOME PROBLEM INTERPOLATIONS.
! SET TRYMERGE TRUE TO USE THIS.
!
TRYMERGE=.TRUE.
NDUMMY=1
NRUNNING=0
MAINLOOP: DO J1=1,SAVENPERMGROUP
   IF (DONEGROUP(J1)) THEN
      NDUMMY=NDUMMY+SAVENPERMSIZE(J1)
      CYCLE MAINLOOP
   ENDIF
!
! PERMUTABLE ATOMS ARE ONLY ALLOWED TO BE PART OF ONE RIGID BODY. OTHER ATOMS CAN BELONG TO
! MORE THAN ONE. HENCE WE FIX ON THE FIRST OPTIMISED PERMUTATION, BUT ALLOW OTHER ATOMS TO
! OVERLAP.
!
   DO J2=1,SAVENPERMSIZE(J1)
      IF (RBASSIGNED(SAVEPERMGROUP(NDUMMY+J2-1))) THEN
         DONEGROUP(J1)=.TRUE.
         IF (DEBUG) PRINT '(A,I6,A)',' MINPERMRB> ATOM ',SAVEPERMGROUP(NDUMMY+J2-1),' IS ALREADY IN AN RB GROUP'
         NDUMMY=NDUMMY+SAVENPERMSIZE(J1)
         CYCLE MAINLOOP
      ENDIF
   ENDDO
   DONEGROUP(J1)=.TRUE.
   NPERMGROUP=1
   PATOMS=SAVENPERMSIZE(J1)
   NPERMSIZE(1)=SAVENPERMSIZE(J1)
   NSETS(1)=SAVENSETS(J1)
   INVMAP(1:SAVENATOMS)=-1

   DO J2=1,SAVENPERMSIZE(J1)
      PERMGROUP(J2)=J2
      ATOMMAP(J2)=SAVEPERMGROUP(NDUMMY+J2-1)
      INVMAP(SAVEPERMGROUP(NDUMMY+J2-1))=J2
      PDUMMYA(3*(J2-1)+1)=COORDSA(3*(SAVEPERMGROUP(NDUMMY+J2-1)-1)+1)
      PDUMMYA(3*(J2-1)+2)=COORDSA(3*(SAVEPERMGROUP(NDUMMY+J2-1)-1)+2)
      PDUMMYA(3*(J2-1)+3)=COORDSA(3*(SAVEPERMGROUP(NDUMMY+J2-1)-1)+3)
      PDUMMYB(3*(J2-1)+1)=COORDSB(3*(SAVEPERMGROUP(NDUMMY+J2-1)-1)+1)
      PDUMMYB(3*(J2-1)+2)=COORDSB(3*(SAVEPERMGROUP(NDUMMY+J2-1)-1)+2)
      PDUMMYB(3*(J2-1)+3)=COORDSB(3*(SAVEPERMGROUP(NDUMMY+J2-1)-1)+3)
   ENDDO
   
   NCOUNT=SAVENPERMSIZE(J1)
   IF (SAVENSETS(J1).GT.0) THEN
      PRINT '(A)',' MINPERMRB> ERROR *** ADDITIONAL ATOM SWAPS NOT ALLOWED FOR RIGID BODIES'
      STOP
   ENDIF

   NATOMS=PATOMS+2*SAVENSETS(J1)
   NSAVE=NATOMS
!
! NOW CHECK IF ANY OF THE ATOMS IN QUESTION LIE IN ADDITIONAL GROUPS THAT CAN BE PERMUTED,
! SUCH AS THE TWO SETS OF METHYL HYDROGENS IN VALINE. IF SO, WE NEED TO ADD THIS PERMUTATION
! INFORMATION. THIS SITUATION CAN ONLY OCCUR IF NSETS(1) > 0.
!
   IF (NSETS(1).GT.0) THEN
      NNDUMMY=0
      NNNDUMMY=NPERMSIZE(1)
      OUTER: DO J2=1,SAVENPERMGROUP
         IF (J2.EQ.J1) THEN
            NNDUMMY=NNDUMMY+SAVENPERMSIZE(J2)
            CYCLE OUTER
         ENDIF
         DO J3=1,SAVENPERMSIZE(J2)
            DO J4=1,NATOMS
               IF (ATOMMAP(J4).EQ.SAVEPERMGROUP(NNDUMMY+J3)) THEN
                  NPERMGROUP=NPERMGROUP+1
                  NPERMSIZE(NPERMGROUP)=SAVENPERMSIZE(J2)
!
!  THERE CAN;T BE ANY SWAPS ASSOCIATED WITH THIS GROUP.
!
                  IF (SAVENSETS(J2).GT.0) THEN
                     IF (DEBUG) PRINT '(A,I6,A,I6)','MINPERMRB> ERROR *** NUMBER OF SETS ASSOCIATED WITH GROUP ', & 
  &                                                  J2,' IS ',SAVENSETS(J2)
                     STOP
                  ENDIF
                  NSETS(NPERMGROUP)=0
                  DO J5=1,SAVENPERMSIZE(J2)
                     PERMGROUP(NNNDUMMY+J5)=INVMAP(SAVEPERMGROUP(NNDUMMY+J5))
                  ENDDO
                  NNNDUMMY=NNNDUMMY+SAVENPERMSIZE(J2)
                  NNDUMMY=NNDUMMY+SAVENPERMSIZE(J2)
                  DONEGROUP(J2)=.TRUE.
                  CYCLE OUTER
               ENDIF
            ENDDO
         ENDDO
         NNDUMMY=NNDUMMY+SAVENPERMSIZE(J2)
      ENDDO OUTER
   ENDIF

   IF (DEBUG) THEN
      PRINT '(A,I6,A,I6)',' MINPERMRB> NUMBER OF SETS OF PERMUTABLE ATOMS FOR GROUP ',J1, ' IS ',NPERMGROUP
      NNDUMMY=1
      DO J2=1,NPERMGROUP
         IF (DEBUG) THEN
            DO J3=1,NPERMSIZE(J2)
               WRITE(*,'(I6)',ADVANCE='NO') ATOMMAP(PERMGROUP(NNDUMMY-1+J3))
            ENDDO
            PRINT '(A)',' '
         ENDIF
         NNDUMMY=NNDUMMY+NPERMSIZE(J2)
      ENDDO
   ENDIF
!
! ADD ANY ATOMS THAT ARE OUTSIDE THE CURRENT PERMUTABLE GROUPS BUT ARE EQUIDISTANT FROM THE 
! ATOMS THAT CAN BE PERMUTED.
! FORBID MEMBERS OF OTHER PERMUTATIONAL GROUPS TO AVOID A SUBSET OF SUCH ATOMS BEING INCLUDED,
! WHICH MAKES THE BOOKKEEPING TRICKY.
!
   NEXTRA=0
   SAVEDIST(1:SAVENATOMS)=-1.0D0
   OUTER2: DO J2=1,SAVENATOMS
      NPERMTOTAL=0
      DO J4=1,SAVENPERMGROUP
         DO J5=1,SAVENPERMSIZE(J4)
            IF (SAVEPERMGROUP(NPERMTOTAL+J5).EQ.J2) CYCLE OUTER2
         ENDDO
         NPERMTOTAL=NPERMTOTAL+SAVENPERMSIZE(J4)
      ENDDO
      DO J4=1,NPERMSIZE(1)
         DISTA(J4)=SQRT( &
  &                (COORDSA(3*(J2-1)+1)-PDUMMYA(3*(J4-1)+1))**2 &
  &               +(COORDSA(3*(J2-1)+2)-PDUMMYA(3*(J4-1)+2))**2 &
  &               +(COORDSA(3*(J2-1)+3)-PDUMMYA(3*(J4-1)+3))**2 )
         IF (DISTA(J4).GT.RBCUTOFF) CYCLE OUTER2
         DISTB(J4)=SQRT( &
  &                (COORDSB(3*(J2-1)+1)-PDUMMYB(3*(J4-1)+1))**2 &
  &               +(COORDSB(3*(J2-1)+2)-PDUMMYB(3*(J4-1)+2))**2 &
  &               +(COORDSB(3*(J2-1)+3)-PDUMMYB(3*(J4-1)+3))**2 )
         IF (DISTB(J4).GT.RBCUTOFF) CYCLE OUTER2
      ENDDO
!
! SORT THE DISTANCES FOR COMPARISON. THEY DO NOT HAVE TO BE EQUIDISTANT FROM THE
! PERMUTABLE ATOMS IN THE TWO END POINTS. THIS ALLOWS ROTATION OF A WHOLE ARGININE.
! TO REDUCE THE ALLOWED RB SIZE JUST REDUCE THE CUTOFF RADIUS RBCUTOFF.
!
      CALL RBSORT(NPERMSIZE(1),DISTA)
      CALL RBSORT(NPERMSIZE(1),DISTB)
      DO J4=1,NPERMSIZE(1)
         IF (ABS(DISTA(J4)-DISTB(J4)).GT.LPDGEOMDIFFTOL) CYCLE OUTER2
      ENDDO
      
      XDUMMY=0.5D0*(SUM(DISTA(1:NPERMSIZE(1)))+SUM(DISTB(1:NPERMSIZE(1))))/NPERMSIZE(1)
!     IF (DEBUG) PRINT '(A,I6,A,F15.5)',' MINPERMRB> ATOM ',J2,' SATISFIES EQUIDISTANCE CONDITION WITH MEAN DISTANCE ',XDUMMY
!     IF (DEBUG) PRINT '(6F15.5)',DISTA(1:NPERMSIZE(1)),DISTB(1:NPERMSIZE(1))
!
!  MAKE AN ORDERED LIST WITH FURTHEST LAST SO THAT WE CAN REMOVE THE FURTHEST ATOMS
!  ONE AT A TIME OF THE ALIGNMENT ISN;T GOOD ENOUGH.
!
      IF ((NEXTRA.EQ.0).OR.(XDUMMY.GT.SAVEDIST(NEXTRA))) THEN
         NATOMS=NATOMS+1
         ATOMMAP(NATOMS)=J2
         PDUMMYA(3*(NATOMS-1)+1:3*(NATOMS-1)+3)=COORDSA(3*(J2-1)+1:3*(J2-1)+3)
         PDUMMYB(3*(NATOMS-1)+1:3*(NATOMS-1)+3)=COORDSB(3*(J2-1)+1:3*(J2-1)+3)
         NEXTRA=NEXTRA+1
         SAVEDIST(NEXTRA)=XDUMMY
      ELSE
         REORDER: DO J4=NSAVE+1,NSAVE+NEXTRA
            IF (XDUMMY.LT.SAVEDIST(J4-NSAVE)) THEN
               DO J5=NATOMS+1,J4+1,-1 ! MOVE THE OTHER ENTRIES UP ONE
                  ATOMMAP(J5)=ATOMMAP(J5-1)
                  PDUMMYA(3*(J5-1)+1:3*(J5-1)+3)=PDUMMYA(3*(J5-2)+1:3*(J5-2)+3)
                  PDUMMYB(3*(J5-1)+1:3*(J5-1)+3)=PDUMMYB(3*(J5-2)+1:3*(J5-2)+3)
                  SAVEDIST(J5-NSAVE)=SAVEDIST(J5-NSAVE-1)
               ENDDO
               ATOMMAP(J4)=J2
               PDUMMYA(3*(J4-1)+1:3*(J4-1)+3)=COORDSA(3*(J2-1)+1:3*(J2-1)+3)
               PDUMMYB(3*(J4-1)+1:3*(J4-1)+3)=COORDSB(3*(J2-1)+1:3*(J2-1)+3)
               SAVEDIST(J4-NSAVE)=XDUMMY
               EXIT REORDER
            ENDIF
         ENDDO REORDER
         NATOMS=NATOMS+1
         NEXTRA=NEXTRA+1
      ENDIF
   ENDDO OUTER2

20 CONTINUE
   XDUMMY=GEOMDIFFTOL
   GEOMDIFFTOL=NPERMSIZE(1)*LPDGEOMDIFFTOL 
!  GEOMDIFFTOL=LPDGEOMDIFFTOL
   PDUMMYASAVE(1:3*NATOMS)=PDUMMYA(1:3*NATOMS)
   IF (DEBUG) THEN
      PRINT '(A,I6,A)',' MINPERMRB> CALLING MINPERMDIST FOR ',NATOMS,' ATOMS:'
      DO J5=1,NATOMS
         WRITE(*,'(I6)',ADVANCE='NO') ATOMMAP(J5)
      ENDDO
      WRITE(*,'(A)') ' '
      PRINT '(A,I6,A)',' MINPERMRB> DISTANCES OF ',NEXTRA,' EXTRA ATOMS:'
      DO J5=1,NEXTRA
         WRITE(*,'(F15.5)',ADVANCE='NO') SAVEDIST(J5)
      ENDDO
      WRITE(*,'(A)') ' '
   ENDIF
   CALL MINPERMDIST(PDUMMYB,PDUMMYA,NATOMS,DEBUG,BOXLX,BOXLY,BOXLZ,BULKT,TWOD,DISTANCE,DIST2,RIGID,RMATBEST)
   GEOMDIFFTOL=XDUMMY
!
! LPDGEOMDIFFTOL IS COMPARED TO THE LARGEST DIFFERENCE FOR A SINGLE ATOM IN THE ALIGNED STRUCTURES.
!
   XDUMMY=-1.0D0
   DO J2=1,NATOMS
      LDUMMY= (PDUMMYB(3*(J2-1)+1)-PDUMMYA(3*(J2-1)+1))**2 &
  &          +(PDUMMYB(3*(J2-1)+2)-PDUMMYA(3*(J2-1)+2))**2 &
  &          +(PDUMMYB(3*(J2-1)+3)-PDUMMYA(3*(J2-1)+3))**2 
      IF (LDUMMY.GT.XDUMMY) XDUMMY=LDUMMY
   ENDDO
   LDUMMY=SQRT(LDUMMY)

   IF (DEBUG) PRINT '(A,I6,2(A,G14.4))',' MINPERMRB> DISTANCE FOR PERMUTABLE GROUP ',J1,' IS ',DISTANCE, &
  &                                  ' LARGEST DISPLACEMENT ',LDUMMY
!  IF ((DISTANCE.GT.NPERMSIZE(1)*LPDGEOMDIFFTOL).AND.(NATOMS.GT.PATOMS+2*SAVENSETS(J1))) THEN
   IF ((LDUMMY.GT.LPDGEOMDIFFTOL).AND.(NATOMS.GT.PATOMS+2*SAVENSETS(J1))) THEN
      NATOMS=NATOMS-1
      IF (DEBUG) PRINT '(A)',' MINPERMRB> DISTANCE IS TOO LARGE - REDUCE NUMBER OF EQUIDISTANT ATOMS BY ONE AND TRY AGAIN'
      PDUMMYA(1:3*NATOMS)=PDUMMYASAVE(1:3*NATOMS)
      GOTO 20
   ENDIF
   PDUMMYA(1:3*SAVENATOMS)=COORDSA(1:3*SAVENATOMS)
   NRBGROUP=NRBGROUP+1
   IF (DEBUG) PRINT '(A,I6,A,I6)',' MINPERMRB> ',NATOMS,' ATOMS ASSIGNED TO RIGID BODY GROUP ',NRBGROUP
   RBNINGROUP(NRBGROUP)=NATOMS
   DO J2=1,NATOMS
      RBASSIGNED(ATOMMAP(J2))=.TRUE.
      IF (NRUNNING+1.GT.RBATOMSMAX) THEN
         ALLOCATE(RBTEMP(NRUNNING))
         RBTEMP(1:NRUNNING)=RBGROUP(1:NRUNNING)
         DEALLOCATE(RBGROUP)
         ALLOCATE(RBGROUP(2*RBATOMSMAX))
         RBGROUP(1:NRUNNING)=RBTEMP(1:NRUNNING)
         DEALLOCATE(RBTEMP)
         RBATOMSMAX=2*RBATOMSMAX
      ENDIF
      NRUNNING=NRUNNING+1
      RBGROUP(NRUNNING)=ATOMMAP(J2)
      PDUMMYA(3*(ATOMMAP(J2)-1)+1)=COORDSA(3*(ATOMMAP(BESTPERM(J2))-1)+1)
      PDUMMYA(3*(ATOMMAP(J2)-1)+2)=COORDSA(3*(ATOMMAP(BESTPERM(J2))-1)+2)
      PDUMMYA(3*(ATOMMAP(J2)-1)+3)=COORDSA(3*(ATOMMAP(BESTPERM(J2))-1)+3)
   ENDDO
   COORDSA(1:3*SAVENATOMS)=PDUMMYA(1:3*SAVENATOMS)

   NDUMMY=NDUMMY+SAVENPERMSIZE(J1)
ENDDO MAINLOOP

IF (.NOT.TRYMERGE) GOTO 111
!
! NOW CHECK IF WE CAN MERGE ANY OF THESE GROUPS. THIS COULD HAPPEN BECAUSE ONLY ONE PERMUTABLE 
! SET IS INITIALLY ALLOWED IN EACH RIGID BODY. NOTE THAT THERE COULD BE OVERALPPING ATOMS BETWEEN
! RIGID BODY GROUPS AS WELL.
! ONLY ALLOW MERGING FOR OVERLAPPING GROUPS.
!
!       IF (DEBUG) THEN
!           PRINT '(3(A,I6))',' MINPERMRB> ORIGINAL RBGROUP ENTRIES:'
!           NDUMMY=0
!           DO J3=1,NRBGROUP
!              PRINT '(A,I6)',' GROUP ',J3 
!              PRINT '(22I6)',RBGROUP(NDUMMY+1:NDUMMY+RBNINGROUP(J3))
!              NDUMMY=NDUMMY+RBNINGROUP(J3)
!           ENDDO
!       ENDIF
30 CONTINUE
NRUNNING1=0
DO J1=1,NRBGROUP
   NRUNNING2=NRUNNING1+RBNINGROUP(J1)
   DO J2=1,RBNINGROUP(J1)
      J3=RBGROUP(J2+NRUNNING1)
      ATOMMAP(J2)=J3
      PDUMMYA(3*(J2-1)+1:3*(J2-1)+3)=COORDSA(3*(J3-1)+1:3*(J3-1)+3)
      PDUMMYB(3*(J2-1)+1:3*(J2-1)+3)=COORDSB(3*(J3-1)+1:3*(J3-1)+3)
   ENDDO

   DO J2=J1+1,NRBGROUP
      NATOMS=RBNINGROUP(J1)
!
! CHECK FOR UNIQUE ATOMS IN SECOND GROUP.
!
      G2ATOMS: DO J5=1,RBNINGROUP(J2)
         J3=RBGROUP(J5+NRUNNING2)
         DO J4=1,RBNINGROUP(J1)
            IF (J3.EQ.RBGROUP(J4+NRUNNING1)) CYCLE G2ATOMS
         ENDDO
         NATOMS=NATOMS+1
         ATOMMAP(NATOMS)=J3
         PDUMMYA(3*(NATOMS-1)+1:3*(NATOMS-1)+3)=COORDSA(3*(J3-1)+1:3*(J3-1)+3)
         PDUMMYB(3*(NATOMS-1)+1:3*(NATOMS-1)+3)=COORDSB(3*(J3-1)+1:3*(J3-1)+3)
      ENDDO G2ATOMS
!
! ONLY TRY TO MERGE GROUPS THAT HAVE A COMMON ATOM.
!
      IF (NATOMS.EQ.RBNINGROUP(J1)+RBNINGROUP(J2)) THEN
         NRUNNING2=NRUNNING2+RBNINGROUP(J2)
         CYCLE
      ENDIF

      XDUMMY=GEOMDIFFTOL
      GEOMDIFFTOL=NPERMSIZE(1)*LPDGEOMDIFFTOL
!     IF (DEBUG) THEN
!        PRINT '(3(A,I6))',' MINPERMRB> CALLING MINPERMDIST FOR ',NATOMS,' ATOMS MERGED FROM GROUPS ',J1,' AND ',J2
!        DO J5=1,NATOMS
!           WRITE(*,'(I6)',ADVANCE='NO') ATOMMAP(J5)
!        ENDDO
!        WRITE(*,'(A)') ' '
!     ENDIF
      CALL MINPERMDIST(PDUMMYB,PDUMMYA,NATOMS,DEBUG,BOXLX,BOXLY,BOXLZ,BULKT,TWOD,DISTANCE,DIST2,RIGID,RMATBEST)
      GEOMDIFFTOL=XDUMMY
!
! LPDGEOMDIFFTOL IS COMPARED TO THE LARGEST DIFFERENCE FOR A SINGLE ATOM IN THE ALIGNED STRUCTURES.
!
      XDUMMY=-1.0D0
      DO J3=1,NATOMS
         LDUMMY= (PDUMMYB(3*(J3-1)+1)-PDUMMYA(3*(J3-1)+1))**2 &
  &             +(PDUMMYB(3*(J3-1)+2)-PDUMMYA(3*(J3-1)+2))**2 &
  &             +(PDUMMYB(3*(J3-1)+3)-PDUMMYA(3*(J3-1)+3))**2
         IF (LDUMMY.GT.XDUMMY) XDUMMY=LDUMMY
      ENDDO
      LDUMMY=SQRT(XDUMMY) ! BUG FIX 30/7/09 DJW
!     IF (DEBUG) PRINT '(2(A,I6),2(A,G14.4))',' MINPERMRB> DISTANCE FOR MERGED GROUPS ',J1,' AND ',J2,' IS ',DISTANCE, &
! &                                           ' LARGEST DISPLACEMENT ',LDUMMY
!
! IF ALLOWED, ADD NON-DUPLICATE ENTRIES FOR J2 GROUP ONTO THE END OF J1 GROUP, MOVING THE REST OF ENTRIES UP
! APPROPRIATELY. REDUCED NRBGROUP BY ONE, AND START ALL OVER AGAIN.
! OTHERWISE, INCREMENT NRUNNING2 AND TRY MERGING WITH THE NEXT GROUP.
!
      IF (LDUMMY.LT.LPDGEOMDIFFTOL) THEN
         ALLOCATE(TEMPRBGROUP(RBATOMSMAX))
         NCOUNT=0
         DO J3=1,J1-1
            TEMPRBNINGROUP(J3)=RBNINGROUP(J3)
            DO J4=1,RBNINGROUP(J3)
               TEMPRBGROUP(NCOUNT+J4)=RBGROUP(NCOUNT+J4)
            ENDDO
            NCOUNT=NCOUNT+RBNINGROUP(J3)
         ENDDO
         TEMPRBGROUP(NCOUNT+1:NCOUNT+NATOMS)=ATOMMAP(1:NATOMS)
         NDUMMY=NCOUNT+RBNINGROUP(J1)
         NCOUNT=NCOUNT+NATOMS
         RBNINGROUP(J1)=NATOMS
         TEMPRBNINGROUP(J1)=NATOMS
         J3LOOP: DO J3=J1+1,NRBGROUP
            IF (J3.EQ.J2) THEN
               NDUMMY=NDUMMY+RBNINGROUP(J3)
               CYCLE J3LOOP
            ENDIF
            IF (J3.LT.J2) TEMPRBNINGROUP(J3)=RBNINGROUP(J3)
            IF (J3.GT.J2) TEMPRBNINGROUP(J3-1)=RBNINGROUP(J3)
            DO J4=1,RBNINGROUP(J3)
               TEMPRBGROUP(NCOUNT+J4)=RBGROUP(NDUMMY+J4)
            ENDDO
            NCOUNT=NCOUNT+RBNINGROUP(J3)
            NDUMMY=NDUMMY+RBNINGROUP(J3)
         ENDDO J3LOOP
         NRBGROUP=NRBGROUP-1
         RBGROUP(1:NCOUNT)=TEMPRBGROUP(1:NCOUNT)
         RBNINGROUP(1:NRBGROUP)=TEMPRBNINGROUP(1:NRBGROUP)
         DEALLOCATE(TEMPRBGROUP)
         IF (DEBUG) THEN
            PRINT '(3(A,I6))',' MINPERMRB> AFTER MERGING GROUPS ',J1,' AND ',J2,' REMAINING GROUPS=',NRBGROUP
            PRINT '(3(A,I6))',' MINPERMRB> NEW RBGROUP ENTRIES:'
            NDUMMY=0
            DO J3=1,NRBGROUP
               PRINT '(A,I6)',' GROUP ',J3 
               PRINT '(22I6)',RBGROUP(NDUMMY+1:NDUMMY+RBNINGROUP(J3))
               NDUMMY=NDUMMY+RBNINGROUP(J3)
            ENDDO
         ENDIF
         GOTO 30
      ELSE
         NRUNNING2=NRUNNING2+RBNINGROUP(J2)
      ENDIF
   ENDDO
   NRUNNING1=NRUNNING1+RBNINGROUP(J1)
ENDDO

111 CONTINUE

NPERMGROUP=SAVENPERMGROUP
NATOMS=SAVENATOMS
NPERMSIZE(1:NATOMS)=SAVENPERMSIZE(1:NATOMS)
PERMGROUP(1:NATOMS)=SAVEPERMGROUP(1:NATOMS)
NSETS(1:NATOMS)=SAVENSETS(1:NATOMS)

!
! ROUTINES LIKE CHECKPAIR ARE EXPECTING THE MINIMUM DISTANCE TO BE RETURNED.
! HERE WE FIX THE PERMUTATIONS AND OPTIMISE WITH RESPECT TO THE OVERALL ORIENTATION USING
! NEWMINDIST.
!
CALL NEWMINDIST(COORDSB,COORDSA,NATOMS,DISTANCE,BULKT,TWOD,'AX    ',.TRUE.,RIGID,DEBUG,RMAT)

! WRITE(876,'(I4)') SAVENATOMS
! WRITE(876,'(A)') 'COORDSA FINALLY'
! WRITE(876,'(A1,1X,3F20.10)') (ZSYM((J1+2)/3),COORDSA(J1),COORDSA(J1+1),COORDSA(J1+2),J1=1,3*NATOMS,3)
! WRITE(774,'(I4)') SAVENATOMS
! WRITE(774,'(A)') 'COORDSB FINALLY '
! WRITE(774,'(A1,1X,3F20.10)') (ZSYM((J1+2)/3),COORDSB(J1),COORDSB(J1+1),COORDSB(J1+2),J1=1,3*NATOMS,3)
! CLOSE(876)
! CLOSE(774)

! STOP

! CALL POTENTIAL(COORDSA,ENERGY,VNEW,.TRUE.,.FALSE.,RMS,.FALSE.,.FALSE.)
! PRINT '(A,2F20.10)',' MINPERMRB> AT THE END FOR MIN A ENERGY AND RMS ARE: ',ENERGY, RMS
! CALL POTENTIAL(COORDSB,ENERGY,VNEW,.TRUE.,.FALSE.,RMS,.FALSE.,.FALSE.)
! PRINT '(A,2F20.10)',' MINPERMRB> AT THE END FOR MIN B ENERGY AND RMS ARE: ',ENERGY, RMS

DISTANCE=SQRT(DISTANCE)

RETURN
END SUBROUTINE MINPERMRB

!
!     THIS SUBPROGRAM PERFORMS A SORT ON THE INPUT DATA AND
!     ARRANGES IT FROM SMALLEST TO BIGGEST. THE EXCHANGE-SORT
!     ALGORITHM IS USED.
!
SUBROUTINE RBSORT(N,A)
IMPLICIT NONE
INTEGER J1, L, N, J2
DOUBLE PRECISION A(*), TEMP

DO J1=1,N-1
   L=J1
   DO J2=J1+1,N
      IF (A(L).GT.A(J2)) L=J2
   ENDDO
   TEMP=A(L)
   A(L)=A(J1)
   A(J1)=TEMP
ENDDO

RETURN
END SUBROUTINE RBSORT

