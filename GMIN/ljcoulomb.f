C   GMIN: A PROGRAM FOR FINDING GLOBAL MINIMA
C   COPYRIGHT (C) 1999-2006 DAVID J. WALES
C   THIS FILE IS PART OF GMIN.
C
C   GMIN IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C   IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C   THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C   (AT YOUR OPTION) ANY LATER VERSION.
C
C   GMIN IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL,
C   BUT WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C   MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE.  SEE THE
C   GNU GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C   YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C   ALONG WITH THIS PROGRAM; IF NOT, WRITE TO THE FREE SOFTWARE
C   FOUNDATION, INC., 59 TEMPLE PLACE, SUITE 330, BOSTON, MA  02111-1307  USA
C
C
C  ENERGY AND GRADIENT FOR LJ PLUS COULOMB REPULSION ON SOME PARTICLES.
C  MARK MILLER AND MARIE-PIERRE GAIGEOT
C
      SUBROUTINE LJCOUL(X,V,ENERGY,GTEST)
      USE COMMONS
      IMPLICIT NONE
      LOGICAL GTEST
      INTEGER J1, J2, J3, J4
      DOUBLE PRECISION X(3*NATOMS), DIST, V(3*NATOMS), R1,
     1                 R6, ENERGY, DUMMYX, DUMMYY, DUMMYZ, DUMMY,
     2                 COULQ2, COULQ2_ENERGY, R8

C     DIVIDE COULOMB CONTRIBUTION TO ENERGY BY 4.  THIS FACTOR IS CANCELLED AT THE
C     END OF THE ENERGY LOOP BY THE 4 PREFACTOR IN THE LJ PART OF THE POTENTIAL.
      COULQ2 = COULQ * COULQ
      COULQ2_ENERGY = COULQ2 / 4.0D0
  
      ENERGY=0.0D0
      DO J1=1,3*NATOMS 
         V(J1)=0.0D0
      ENDDO
      IF (GTEST) THEN
         DO J1=1,NATOMS
            J3=3*J1
            DO J2=J1+1,NATOMS
               J4=3*J2
               DUMMYX = X(J3-2)-X(J4-2)
               DUMMYY = X(J3-1)-X(J4-1)
               DUMMYZ = X(J3)-X(J4)
               DIST = DUMMYX**2 + DUMMYY**2 + DUMMYZ**2
               DIST=1.0D0/DIST
               R6=DIST**3
               DUMMY=R6*(R6-1.0D0)
               ENERGY=ENERGY+DUMMY
               R8=DIST*R6
               DUMMY = -24.0D0*(2.0D0*R6-1.0D0)*R8
               IF ( (J1.LE.COULN).AND.(J2.LE.COULN) ) THEN
                  R1 = SQRT(DIST)
                  ENERGY = ENERGY + COULQ2_ENERGY*R1
                  DUMMY = DUMMY - COULQ2*R1*DIST
               END IF
               V(J3-2) = V(J3-2) + DUMMY * DUMMYX
               V(J3-1) = V(J3-1) + DUMMY * DUMMYY
               V(J3) = V(J3) + DUMMY * DUMMYZ
               V(J4-2) = V(J4-2) - DUMMY * DUMMYX
               V(J4-1) = V(J4-1) - DUMMY * DUMMYY
               V(J4) = V(J4) - DUMMY * DUMMYZ
            ENDDO
         ENDDO
      ELSE
         DO J1=1,NATOMS
            J3=3*J1
            DO J2=J1+1,NATOMS
               J4=3*J2
               DIST=(X(J3-2)-X(J4-2))**2+(X(J3-1)-X(J4-1))**2+(X(J3)-X(J4))**2
               DIST=1.0D0/DIST
               R6=DIST**3
               DUMMY=R6*(R6-1.0D0)
               ENERGY=ENERGY+DUMMY
               IF ( (J1.LE.COULN).AND.(J2.LE.COULN) ) THEN
                  ENERGY = ENERGY + COULQ2_ENERGY*SQRT(DIST)
               END IF
            ENDDO
         ENDDO
      ENDIF

      ENERGY=ENERGY*4.0D0

      RETURN
      END

C--------------------------------------------------------------------------------

C     ATTEMPT SWAP BETWEEN CHARGED AND NEUTRAL PARTICLES IN THE LJ+COULOMB
C     POTENTIAL.

      SUBROUTINE TAKESTEPLJC(NP)

      USE COMMONS, ONLY : NATOMS, COORDS, COULN
      IMPLICIT NONE
      INTEGER NP

      DOUBLE PRECISION DPRAND

      DOUBLE PRECISION X, Y, Z
      INTEGER J1, J2

      IF (COULN==0 .OR. COULN==NATOMS) RETURN

C     CHOOSE ONE CHARGED PARTICLE (1 TO COULN) AND ONE NEUTRAL (COULN+1 TO NATOMS)
      J1 = 3 * (INT(DPRAND()*COULN))
      J2 = 3 * (INT(DPRAND()*(NATOMS-COULN)) + COULN)

      X = COORDS(J1+1, NP)
      Y = COORDS(J1+2, NP)
      Z = COORDS(J1+3, NP)

      COORDS(J1+1, NP) = COORDS(J2+1, NP)
      COORDS(J1+2, NP) = COORDS(J2+2, NP)
      COORDS(J1+3, NP) = COORDS(J2+3, NP)

      COORDS(J2+1, NP) = X
      COORDS(J2+2, NP) = Y
      COORDS(J2+3, NP) = Z

      RETURN
      END
