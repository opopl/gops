C   GMIN: A PROGRAM FOR FINDING GLOBAL MINIMA
C   COPYRIGHT (C) 1999-2006 DAVID J. WALES
C   THIS FILE IS PART OF GMIN.
C
C   GMIN IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C   IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C   THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C   (AT YOUR OPTION) ANY LATER VERSION.
C
C   GMIN IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL,
C   BUT WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C   MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE.  SEE THE
C   GNU GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C   YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C   ALONG WITH THIS PROGRAM; IF NOT, WRITE TO THE FREE SOFTWARE
C   FOUNDATION, INC., 59 TEMPLE PLACE, SUITE 330, BOSTON, MA  02111-1307  USA
C
C
C      SUTTON-CHEN POTENTIALS
C
      SUBROUTINE SC(X,V,PSC,GRADT)
      USE COMMONS
      IMPLICIT NONE
      INTEGER J1, J2, J3, J4, I, J
      LOGICAL GRADT
      DOUBLE PRECISION X(3*NATOMS), DMM(NATOMS,NATOMS), POTA, POTB, SIGMM, DNN(NATOMS,NATOMS),
     1                 V(3*NATOMS), SCRHO(NATOMS), DUMMY, PSC, TPOTA, TPOTB, SIGNN, D2(NATOMS,NATOMS),
     2                 DUMMY2, CPMDFAC
      LOGICAL EVAP, EVAPREJECT
      COMMON /EV/ EVAP, EVAPREJECT

      EVAP=.FALSE.
      SIGMM=SIG**MM
      SIGNN=SIG**NN
      PSC=0.0D0
      DO J1=1,3*NATOMS
         V(J1)=0.0D0
      ENDDO
      CPMDFAC=1.0D0
C      IF (CPMD) CPMDFAC=0.5291772D0
      DO J1=1,NATOMS
         VT(J1)=0.0D0
         D2(J1,J1)=0.0D0
         DMM(J1,J1)=0.0D0
         DNN(J1,J1)=0.0D0
         J4=3*(J1-1)
         DUMMY=X(J4+1)**2+X(J4+2)**2+X(J4+3)**2
         DUMMY=DUMMY*CPMDFAC**2
C        IF (DUMMY.GT.RADIUS) THEN
C           EVAP=.TRUE.
C           PSC=PSC+(DUMMY-RADIUS)**2
C           IF (GRADT) THEN
C              V(J4+3)=4.0D0*(DUMMY-RADIUS)*X(J4+3)*CPMDFAC
C              V(J4+2)=4.0D0*(DUMMY-RADIUS)*X(J4+2)*CPMDFAC
C              V(J4+1)=4.0D0*(DUMMY-RADIUS)*X(J4+1)*CPMDFAC
C           ENDIF
C        ENDIF
         DO J2=1,J1-1
            J3=3*(J2-1)
            DUMMY=(X(J4+1)-X(J3+1))**2+(X(J4+2)-X(J3+2))**2+(X(J4+3)-X(J3+3))**2
            DUMMY=DUMMY*CPMDFAC**2
            DUMMY=1.0D0/DUMMY
            D2(J2,J1)=DUMMY
            D2(J1,J2)=DUMMY
            DUMMY=DSQRT(DUMMY)
            DMM(J2,J1)=SIGMM*DUMMY**MM
            DMM(J1,J2)=DMM(J2,J1)
            DNN(J2,J1)=SIGNN*DUMMY**NN
            DNN(J1,J2)=DNN(J2,J1)
         ENDDO
      ENDDO
C
C STORE DENSITY MATRIX.
C
      DO I=1,NATOMS
         DUMMY=0.0D00
         DO J=1,NATOMS
            DUMMY=DUMMY + DMM(J,I)
         ENDDO
         SCRHO(I)=DSQRT(DUMMY)
      ENDDO
C
C FIRST CALCULATE THE POTENTIAL ENERGY. CHOOSING SIG=SQRT(2) MAKES THE
C UNIT OF LENGTH THE BULK NEAREST-NEIGHBOUR DISTANCE. CHOOSING SIG TO 
C BE THE BULK LATTICE CONSTANT (=SQRT(2)X NN-DISTANCE) GIVES THE ORIGINAL
C SC FORM. CAN ALSO CHOOSE SIGMA=EPS=1 AND THEN SCALE THE ENERGIES BY EPS
C AND THE DISTANCES BY SIG.
C METAL  NN  MM   SCEPS/EV    SCC   SIG
C  CU     9   6   0.012382  39.432
C  NI     9   6   0.015707  39.432
C  AG    12   6   0.002542  144.41
C  AU    10   8   0.012793  34.408
C  PT    10   8   0.019833  34.408
C  PD    12   7   0.004179  108.27
C
      POTA=0.0D0
      POTB=0.0D0
      DO I=1,NATOMS
         TPOTA=0.0D0
         DO J=1,NATOMS
            TPOTA=TPOTA + DNN(J,I)
         ENDDO
         TPOTA=TPOTA
         POTA=POTA+TPOTA
         TPOTB=SCRHO(I)
         POTB=POTB + TPOTB
         VT(I)=SCEPS*(TPOTA/2.0D0 - TPOTB*SCC)
      ENDDO
      PSC=PSC + SCEPS*(POTA/2.0D0 - POTB*SCC)
C
C     PRINT*,'SUTTON-CHEN N=', NN,' M=', MM
C     PRINT*,'TWO-BODY CONTRIBUTION=',POTA,' EV'
C     PRINT*,'MANY-BODY CONTRIBUTION=',-POTB,' EV'
C     PRINT*,'TOTAL ENERGY FOR LAST STEP=', PSC,' EV'
C
      IF (.NOT.GRADT) RETURN
C
C NOW CALCULATE THE GRADIENT ANALYTICALLY.
C
      DO J1=1,NATOMS
         DUMMY2=1.0D0/SCRHO(J1)
         DO J2=1,3
            J3=3*(J1-1)+J2
            DUMMY=0.0D0
            DO J4=1,NATOMS
               DUMMY=DUMMY+( MM*SCC*DMM(J4,J1)*(DUMMY2 + 1.0D0/SCRHO(J4))/2.0D0
     2                      -NN*DNN(J4,J1) ) * (X(J3)-X(3*(J4-1)+J2))*D2(J4,J1)*CPMDFAC
            ENDDO
            V(J3)=V(J3)+SCEPS*DUMMY
         ENDDO
      ENDDO
C     PRINT*,'ENERGY=',PSC

      RETURN
      END
