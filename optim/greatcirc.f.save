C LBFGS method for optimizing image points on great circle
C        LIMITED MEMORY BFGS METHOD FOR LARGE SCALE OPTIMIZATION
C                          JORGE NOCEDAL
C                        *** July 1990 ***
C  EFK: Copied from mylbfgs.f, 3/11/06
C PROJECT, TWOEND,GRADSQ, INTMIN, CHARMM, DRAGT  options removed
C PV option retained
C 
      SUBROUTINE GCLBFGS(RS,RF,XI,N,M,X,DIAGCO,EPS,MFLAG,ENERGY,RMS,ITMAX,RESET,ITDONE,PTEST)
      USE COMMONS
      USE KEY
      USE MODTWOEND
      USE MODHESS
      USE ZWK
      USE MODUNRES
      USE MODCHARMM
      use porfuncs
      IMPLICIT NONE
      INTEGER N,M,J1,ITMAX,ITDONE,NFAIL,NCOUNT,J2
C     DOUBLE PRECISION X(*),G(3*NATOMS),DIAG(N),W(N*(2*M+1)+2*M),SLENGTH,DDOT,OVERLAP
      DOUBLE PRECISION X(3*NATOMS),G(3*NATOMS),SLENGTH,DDOT,OVERLAP,DISTF,RMAT(3,3)
      DOUBLE PRECISION, ALLOCATABLE, DIMENSION(:) :: DIAG, W
      DOUBLE PRECISION EPS,DUMMY1,ENERGY,ENEW,RMS,ALPHA,GSAVE(3*NATOMS),DIST2
      LOGICAL DIAGCO, RESET, PTEST
      DOUBLE PRECISION GNORM,STP,YS,YY,SQ,YR,BETA
      DOUBLE PRECISION GLAST(3*NATOMS)
      INTEGER ITER,POINT,ISPT,IYPT,BOUND,NPT,CP,I,INMC,IYCN,ISCN
      LOGICAL MFLAG
      DOUBLE PRECISION GSQSCALE, GSTHRESH, DOT1, DOT2
      INTEGER NSPECIAL, NALLOW, NINFO, FRAME
      LOGICAL PVFLAG
      COMMON /PVF/ PVFLAG
      COMMON /G2/ GSTHRESH, GSQSCALE, NSPECIAL, NALLOW, NINFO
      LOGICAL PATHT, DRAGT
      INTEGER NPATHFRAME, NDECREASE, ISTAT
      COMMON /RUNTYPE/ DRAGT, PATHT, NPATHFRAME
      LOGICAL KNOWE, KNOWG, KNOWH
      COMMON /KNOWN/ KNOWE, KNOWG, KNOWH
      CHARACTER ESTRING*87, GPSTRING*80, NSTRING*80, FSTRING*80
      COMMON /STRINGS/ ESTRING, GPSTRING, NSTRING, FSTRING
      LOGICAL RADMOVED, OVERLP
      COMMON /DISCON/ RADMOVED, OVERLP
      LOGICAL PUSH, PULL
      COMMON /MORPHDATA/ PUSH, PULL
      CHARACTER(LEN=5) ZSYMSAVE
      COMMON /SYS/ ZSYMSAVE
C
C  SGI appears to need this SAVE statement!
C
      SAVE W, DIAG, ITER, POINT, ISPT, IYPT, NPT
C
C EFK: specifically for great circle stuff
C
      DOUBLE PRECISION RS(3*NATOMS), RF(3*NATOMS), XI(GCIMAGE,3*NATOMS)

      IF (.NOT.ALLOCATED(DIAG)) ALLOCATE(DIAG(N))       ! SAVE doesn't work otherwise for Sun
      IF (.NOT.ALLOCATED(W)) ALLOCATE(W(N*(2*M+1)+2*M)) ! SAVE doesn't work otherwise for Sun
      IF (SIZE(W,1).NE.N*(2*M+1)+2*M) THEN ! mustn't call mylbfgs with changing number of variables!!!
         PRINT '(A,I10,A,I10,A)', 'ERROR, dimension of W=',SIZE(W,1),
     $        ' but N*(2*M+1)+2*M=',N*(2*M+1)+2*M,' in mylbfgs'
         call exit(10)
      ENDIF

      IF (N.NE.3*NATOMS) THEN
         PRINT*,'ERROR - N and 3*NATOMS are different in mylbfgs: ',N,3*NATOMS
         call exit(10)
      ENDIF

      IF (PERMDIST) THEN
         CALL MINPERMDIST(RS,RF,NATOMS,DEBUG,PARAM1,PARAM2,PARAM3,PERIODIC,TWOD,DISTF,DIST2,RIGIDBODY,RMAT)
      ELSE
         CALL NEWMINDIST(RS,RF,NATOMS,DISTF,BULKT,TWOD,ZSYMSAVE,.FALSE.,RIGIDBODY,DEBUG,RMAT)
      ENDIF

      ALPHA=1.0D0
      NFAIL=0
      FRAME=1
      IF (RESET) ITER=0
      ITDONE=0
      IF (RESET.AND.PTEST) WRITE(*,'(A)') ' Resetting LBFGS minimiser'
      IF ((.NOT.RESET).AND.PTEST) WRITE(*,'(A)')
     $     ' Not resetting LBFGS minimiser'
1     FIXIMAGE=.FALSE.
      IF (PV) THEN
         IF (.NOT.KNOWE) THEN 
            CALL GETENERG(RS, RF, X, 3*NATOMS, GCIMAGE, ENERGY,GSAVE, RMS, XI)

! Check for cold fusion
            if (ENERGY.LT.coldFusionLimit) then
               ENERGY=1.0d6
               RMS=1.0d0
               WRITE(*,'(A)') ' Cold fusion diagnosed - step discarded'
               RETURN
            endif
         ENDIF
         PVFLAG=.FALSE.
         CALL PVOPT(X,ENERGY,GSAVE)
      ENDIF

      IF ((.NOT.KNOWE).OR.(.NOT.KNOWG)) THEN
         CALL GETENERG(RS, RF, X, 3*NATOMS, GCIMAGE, ENERGY, GSAVE, RMS, XI)
         if (ENERGY.LT.coldFusionLimit) then
            ENERGY=1.0d6
            RMS=1.0d0
            WRITE(*,'(A)') ' Cold fusion diagnosed - step discarded'
            RETURN
         endif
      ENDIF

      G(1:3*NATOMS)=GSAVE(1:3*NATOMS)
      GLAST(1:3*NATOMS)=GSAVE(1:3*NATOMS)
C
      IF (PTEST) WRITE(*,'(A,2G20.10,A,I6,A)') 
     1             ' Energy per image and RMS force=',ENERGY/GCIMAGE,RMS,' after ',ITDONE,' LBFGS steps'

10    CALL FLUSH(6,ISTAT)
      IF (DEBUG) THEN
         OPEN(UNIT=45,FILE='greatcircle.xyz',STATUS='UNKNOWN')
         WRITE(45,'(I6)') NATOMS
         WRITE(45,'(A)') ' '
         WRITE(45,'(A3,3G20.10)') ('LA ',RS(3*(J1-1)+1),RS(3*(J1-1)+2),RS(3*(J1-1)+3),J1=1,NATOMS)
         DO J1=1,GCIMAGE
            WRITE(45,'(I6)') NATOMS
            WRITE(45,'(A)') ' '
            WRITE(45,'(A3,3G20.10)') ('LA ',XI(J1,3*(J2-1)+1),XI(J1,3*(J2-1)+2),XI(J1,3*(J2-1)+3),J2=1,NATOMS)
         ENDDO
         WRITE(45,'(I6)') NATOMS
         WRITE(45,'(A)') ' '
         WRITE(45,'(A3,3G20.10)') ('LA ',RF(3*(J1-1)+1),RF(3*(J1-1)+2),RF(3*(J1-1)+3),J1=1,NATOMS)
         CLOSE(45)
      ENDIF
      MFLAG=.FALSE.
      IF (RMS.LE.EPS) THEN
         MFLAG=.TRUE.
C        PRINT*,'RMS,EPS,ITDONE,NSTEPMIN=',RMS,EPS,ITDONE,NSTEPMIN
         IF (ITDONE.LT.NSTEPMIN) MFLAG=.FALSE.
         IF (PV.AND.(.NOT.PVFLAG)) MFLAG=.FALSE.
         IF (MFLAG) THEN
            FIXIMAGE=.FALSE.
            IF (PTEST) WRITE(*,'(1x,a,g25.17)') 'Final energy is ',ENERGY
            RETURN
         ENDIF
      ENDIF

      IF (ITDONE.EQ.ITMAX) THEN
         FIXIMAGE=.FALSE.
         RETURN
      ENDIF

      IF (ITER.EQ.0) THEN
         IF (N.LE.0.OR.M.LE.0) THEN
            WRITE(*,240)
 240        FORMAT(' IMPROPER INPUT PARAMETERS (N OR M ARE NOT POSITIVE)')
            call exit(10)
         ENDIF
         POINT=0
         MFLAG=.FALSE.
         IF (DIAGCO) THEN
            PRINT*,'using estimate of the inverse diagonal elements'
            DO I=1,N
               IF (DIAG(I).LE.0.0D0) THEN
                  WRITE(*,235) I
 235              FORMAT(' THE',I5,'-TH DIAGONAL ELEMENT OF THE',/,
     1                   ' INVERSE HESSIAN APPROXIMATION IS NOT POSITIVE')
                  call exit(10)
               ENDIF
            ENDDO
         ELSE
            DO I=1,N
               DIAG(I)=DGUESS
            ENDDO
         ENDIF
C
C     THE WORK VECTOR W IS DIVIDED AS FOLLOWS:
C     ---------------------------------------
C     THE FIRST N LOCATIONS ARE USED TO STORE THE GRADIENT AND
C         OTHER TEMPORARY INFORMATION.
C     LOCATIONS (N+1)...(N+M) STORE THE SCALARS RHO.
C     LOCATIONS (N+M+1)...(N+2M) STORE THE NUMBERS ALPHA USED
C         IN THE FORMULA THAT COMPUTES H*G.
C     LOCATIONS (N+2M+1)...(N+2M+NM) STORE THE LAST M SEARCH
C         STEPS.
C     LOCATIONS (N+2M+NM+1)...(N+2M+2NM) STORE THE LAST M
C         GRADIENT DIFFERENCES.
C
C     THE SEARCH STEPS AND GRADIENT DIFFERENCES ARE STORED IN A
C     CIRCULAR ORDER CONTROLLED BY THE PARAMETER POINT.
C
         ISPT= N+2*M
         IYPT= ISPT+N*M
C
C  NR step for diagonal inverse Hessian
C
         
         DO I=1,N
            W(ISPT+I)= -G(I)*DIAG(I)
            W(I)= -G(I)*DIAG(I)
         ENDDO
         GNORM= DSQRT(DDOT(N,G,1,G,1))

C
C  Make the first guess for the step length cautious.
C
         IF (GNORM.EQ.0.0D0) THEN
            GNORM=1.0D0 ! exact zero is presumably wrong!
            PRINT '(A)','WARNING - GNORM was zero in mylbfgs,
     $           resetting to one'
         ENDIF
         STP=MIN(1.0D0/GNORM,GNORM)
      ELSE 
         BOUND=ITER
         IF (ITER.GT.M) BOUND=M
         YS= DDOT(N,W(IYPT+NPT+1),1,W(ISPT+NPT+1),1)
         IF (YS.EQ.0.0D0) YS=1.0D0
C
C  Update estimate of diagonal inverse Hessian elements
C  We divide by both YS and YY at different points, so
C  they had better not be zero!
C
         IF (.NOT.DIAGCO) THEN
            YY= DDOT(N,W(IYPT+NPT+1),1,W(IYPT+NPT+1),1)
            IF (YY.EQ.0.0D0) YY=1.0D0
            DUMMY1=YS/YY
            DO I=1,N
               DIAG(I)=DUMMY1
            ENDDO
         ELSE
            PRINT*,'using estimate of the inverse diagonal elements'
            DO I=1,N
               IF (DIAG(I).LE.0.0D0) THEN
                  WRITE(*,235) I
                  call exit(10)
               ENDIF
            ENDDO
         ENDIF
C
C     COMPUTE -H*G USING THE FORMULA GIVEN IN: Nocedal, J. 1980,
C     "Updating quasi-Newton matrices with limited storage",
C     Mathematics of Computation, Vol.24, No.151, pp. 773-782.
C     ---------------------------------------------------------
C
         CP= POINT
         IF (POINT.EQ.0) CP=M
         W(N+CP)= 1.0D0/YS         
         DO I=1,N
            W(I)= -G(I)
         ENDDO
         CP= POINT
         DO I= 1,BOUND
            CP=CP-1
            IF (CP.EQ. -1)CP=M-1
            SQ= DDOT(N,W(ISPT+CP*N+1),1,W,1)
            INMC=N+M+CP+1
            IYCN=IYPT+CP*N
            W(INMC)= W(N+CP+1)*SQ
            CALL DAXPY(N,-W(INMC),W(IYCN+1),1,W,1)
         ENDDO
        
         DO I=1,N
            W(I)=DIAG(I)*W(I)
         ENDDO

         DO I=1,BOUND
            YR= DDOT(N,W(IYPT+CP*N+1),1,W,1)
            BETA= W(N+CP+1)*YR
            INMC=N+M+CP+1
            BETA= W(INMC)-BETA
            ISCN=ISPT+CP*N
            CALL DAXPY(N,BETA,W(ISCN+1),1,W,1)
            CP=CP+1
            IF (CP.EQ.M) CP=0
         ENDDO
         STP=1.0D0
      ENDIF
C
C  If this is a BFGSTST or MORPHT  run project out the uphill direction.
C     PRINT '(A,6G20.10)','W=',W(1:6)
C
C  Store the new search direction
      IF (ITER.GT.0) THEN
         DO I=1,N
            W(ISPT+POINT*N+I)= W(I)
         ENDDO
      ENDIF
      DOT1=SQRT(DDOT(N,G,1,G,1))
      DOT2=SQRT(DDOT(N,W,1,W,1))
      OVERLAP=0.0D0
      IF (DOT1*DOT2.NE.0.0D0) THEN         
         OVERLAP=DDOT(N,G,1,W,1)/(DOT1*DOT2)
      ENDIF
C
      IF (OVERLAP.GT.0.0D0) THEN
         IF (PTEST) PRINT*,'Search direction has positive
     $        projection onto gradient - reversing step'
         DO I=1,N
            W(ISPT+POINT*N+I)= -W(I)  ! if we reverse the step it is important not to take the ABS value of YS/YY!
         ENDDO
C        ITER=0
C        GOTO 10
      ENDIF      
      DO I=1,N
         W(I)=G(I)
      ENDDO
      SLENGTH=0.0D0
      DO J1=1,N
         SLENGTH=SLENGTH+W(ISPT+POINT*N+J1)**2
      ENDDO
      SLENGTH=SQRT(SLENGTH)
      IF (STP*SLENGTH.GT.GCMXSTP) STP=MAXBFGS/SLENGTH
C
C  We now have the proposed step.
C      
      GNORM= DSQRT(DDOT(N,G,1,G,1))
      DO J1=1,N
         X(J1)=X(J1)+STP*W(ISPT+POINT*N+J1)
      ENDDO 
C
      KNOWE=.FALSE.
      KNOWG=.FALSE.
      KNOWH=.FALSE.
C
C At this point we have new Cartesian or internal coordinates after taking a full
C or decreased step. The gradient is not known at this geometry.
C
      NDECREASE=0
      NCOUNT=0

 20   IF (PV) THEN
         CALL GETENERG(RS, RF, X, 3*NATOMS, GCIMAGE, ENEW, GSAVE, RMS, XI)
C         CALL POTENTIAL(X,ENEW,GSAVE,.FALSE.,.FALSE.,RMS,.FALSE.,.FALSE.)
         PVFLAG=.FALSE.
         CALL PVOPT(X,ENEW,GSAVE)
      ENDIF
      CALL GETENERG(RS, RF, X, 3*NATOMS, GCIMAGE, ENEW, GSAVE, RMS, XI)

      if (ENERGY.LT.coldFusionLimit) then
         ENERGY=1.0d6
         RMS=1.0d0
         WRITE(*,'(A)') ' Cold fusion diagnosed - step discarded'
         RETURN
      endif

      G(1:3*NATOMS)=GSAVE(1:3*NATOMS)

C
C  Must allow the energy to rise during a minimisation to allow for numerical noise or
C  systematic errors due to discontinuities or SCF convergence problems.
C
      IF ((ENEW-ENERGY.LE.MAXERISE).OR.PVTS.OR.DRAGT.OR.TWOENDS.OR.RADMOVED) THEN
         ITER=ITER+1
         ITDONE=ITDONE+1
         ENERGY=ENEW

         IF (PTEST) WRITE(*,'(A,2G20.10,A,I6,A,G13.5)')
     $        ' Energy and RMS force=',ENERGY,RMS,' after ',ITDONE,
     1           ' LBFGS steps, step:',STP*SLENGTH
C
C  Step finished so can reset OLDQ to new XINT, OLDCART to new CART,
C  as well as the Cartesian and internal gradients.
C         
         GLAST(1:3*NATOMS)=GSAVE(1:3*NATOMS) 
      ELSE 
C
C  Energy increased - try again with a smaller step size. Must cater for possible enormous
C  values of SLENGTH. Decreasing the step size doesn;t seem to help for CASTEP.
C
         IF (((ITER.GT.1).AND.(NDECREASE.GT.2)).OR.((ITER.LE.1).AND.(NDECREASE.GT.10)).OR.
     1              (CASTEP.AND.(NDECREASE.GT.1))) THEN
            NFAIL=NFAIL+1
            IF (PTEST) PRINT*,' in mylbfgs LBFGS step
     $           cannot find a lower energy, NFAIL=',NFAIL
C
C  try resetting - go back to previous coordinates, ENERGY is not set to ENEW
C  we need to save the gradient corresponding to the last successful step
C              
            ITER=0            
            DO J1=1,N
               X(J1)=X(J1)-STP*W(ISPT+POINT*N+J1)
               G(J1)=GLAST(J1)
            ENDDO
            IF (NFAIL.GT.NFAILMAX) THEN
               PRINT*,' Too many failures - give up'
               FIXIMAGE=.FALSE.
               RETURN
            ENDIF
            GOTO 30
         ENDIF
C
C  Try a smaller step.
C         
         DO J1=1,N
            X(J1)=X(J1)-0.9*STP*W(ISPT+POINT*N+J1)
         ENDDO 
         KNOWE=.FALSE.
         KNOWG=.FALSE.
         KNOWH=.FALSE.
         STP=STP/10.0D0
         NDECREASE=NDECREASE+1
         IF (PTEST) 
     1    WRITE(*,'(A,G19.10,A,G16.10,A,G15.8)')
     $        ' energy increased from ',ENERGY,' to ',ENEW,
     2            ' decreasing step to ',STP*SLENGTH
         GOTO 20
      ENDIF
C
C     Compute the new step and gradient change. Note that the step
C     length is accounted for when the step taken is saved.
C
30    NPT=POINT*N
      
      DO I=1,N
         W(ISPT+NPT+I)= STP*W(ISPT+NPT+I)
         W(IYPT+NPT+I)= G(I)-W(I)
      ENDDO
      POINT=POINT+1
      IF (POINT.EQ.M) POINT=0
      FIXIMAGE=.FALSE.
      IF ((FIXAFTER.GT.0).AND.(ITER.GE.FIXAFTER)) FIXIMAGE=.TRUE.
      GOTO 10

      RETURN
      END

      SUBROUTINE GETENERG(RS, RF, V, NC, NI, TOTENERG, dTEdV, RMS, XI)
C
C Subroutine to get the total energy of the image points on a particular 
C great circle and the derivatives of the energy wrt hypersphere center
C RS and RF are start and finish geometries
C V is the displacement of the hypersphere center from the linear center
C NC is the number of coordinates
C NI is the number of image points
C TOTENERG is the output total energy of the image points
C dTEdV is the gradient of the total energy wrt to hypersphere center
C RMS is the norm of dTEdV
C
      IMPLICIT NONE
      INTEGER NC, NI
      DOUBLE PRECISION RS(NC), RF(NC), V(NC), TOTENERG, dTEdV(NC)
      DOUBLE PRECISION XI(NI,NC), dXIdV(NI,NC,NC)
      DOUBLE PRECISION ENERG, dEdX(NC)
      DOUBLE PRECISION RMS
      INTEGER I, A, B
C     DOUBLE PRECISION TINY
      TOTENERG = 0.0D0
      dTEdV = 0.0D0

C     Get each image point and derivatives
      CALL GETIMGDERVS(RS, RF, V, NC, NI, XI, dXIdV)
C     Calculate energies and energy derivatives
      DO I=1,NI
         CALL POTENTIAL(XI(I,:), ENERG, dEdX, .TRUE., .FALSE., RMS,
     $        .FALSE., .FALSE.)
         TOTENERG = TOTENERG+ENERG
         DO B = 1,NC
            DO A = 1,NC            
               dTEdV(B) = dTEdV(B) + dEdX(A)*dXIdV(I,A,B)
            ENDDO
         ENDDO
      ENDDO
      RMS = 0.0D0
      DO A=1,NC
         RMS = RMS + dTEdV(A)**2
      ENDDO
      RMS = SQRT(RMS)

C ----TESTING ----
C test energy derivatives wrt V
C      TINY = 0.001
C      DO J = 1,NC
C         V(J) = V(J) + TINY
C         TOTENERG2=0.0D0
C         dTEdV2 = 0.0D0
C         DO I = 1, NI     
C     Get each image point and derivatives
C            CALL GETIMGDERVS(RS, RF, V, I, NC, NI, XI, dXIdV)
C     Calculate energy and energy derivatives at this point
C            CALL POTENTIAL(XI, ENERG, dEdX, .TRUE., .FALSE., RMS,
C     $           .FALSE., .FALSE.)
C            TOTENERG2 = TOTENERG2+ENERG
C            DO B = 1,NC
C               DO A = 1,NC            
C                  dTEdV2(B) = dTEdV2(B) + dEdX(A)*dXIdV(A,B)
C               ENDDO
C            ENDDO
C         ENDDO
C          print*, J, (TOTENERG2-TOTENERG)/TINY, dTEdV(J)
C         V(J) = V(J) - TINY
C      ENDDO
C -------------
C compare to linear interpolation
C      DO I=1,NC
C         testc(I) = (rs(I)+rf(I))/2
C      ENDDO
C      
C      DO I=1,NC
C         print*, testc(I), XI(I)
C      ENDDO
C
      END SUBROUTINE GETENERG
C

      SUBROUTINE GETIMGDERVS(RS, RF, V, NC, NI, XI, dXIdV)
C Subroutine to get the interpolation points Xi on a particular 
C great circle, and the derivatives dXia/dCb
C with respect to the hypersphere center.
C RS and RF are the start and finish geometries
C V is a particular shift of the hypersphere center 
C I gives the index of the interpolation point
C NC is the number of coordinates
C N is the number of interpolation points
C DXDC is the ouput matrix of derivatives

      IMPLICIT NONE
      INTEGER I, NC, NI
      DOUBLE PRECISION RS(NC), RF(NC), V(NC), dXIdV(NI,NC,NC), XI(NI,NC)
      DOUBLE PRECISION TINY
      PARAMETER(TINY = 0.000001)
      DOUBLE PRECISION RSF(NC), RSF2, RSFR, VRSF, VV
      DOUBLE PRECISION dCdV(NC,NC), dROdV(NC), dV1dV(NC,NC), dTFdV(NC), dV2dV(NC,NC)
      DOUBLE PRECISION C(NC), RO, RO2, ROR, V1(NC), TF, V2(NC)
      DOUBLE PRECISION DUMMY, CTF, STF
      DOUBLE PRECISION STI, CTI
      INTEGER A,B, J
      DOUBLE PRECISION COFFSET

C Get difference vector RSF, RSF^2, V^2, and V*RSF
      RSF2 = 0.0D0
      VV = 0.0D0
      VRSF = 0.0D0
      DO J=1,NC
         RSF(J) = RS(J)-RF(J)
         RSF2 = RSF2 + RSF(J)**2
         VV = VV + V(J)**2
         VRSF = VRSF + V(J)*RSF(J)
      ENDDO
      RSFR = 1/SQRT(RSF2)
C Get hypersphere center and its derivatives
      COFFSET = 0.0D0
      DO A=1,NC         
         C(A) = (RS(A)+RF(A))/2 + V(A) - VRSF*RSF(A)/RSF2
         COFFSET=COFFSET+(V(A)-VRSF*RSF(A)/RSF2)**2
      ENDDO
      IF(COFFSET.LT.1.0D-8) print*, 'SMALL CENTER OFFSET:', COFFSET
      DO A=1,NC
         DO B= 1,NC
            dCdV(A,B) = -RSF(A)*RSF(B)/RSF2
         ENDDO
         dCdV(A,A) = dCdV(A,A) + 1
      ENDDO
C Get radius of circle & its derivatives
      RO2 = RSF2/4 + VV - VRSF*VRSF/RSF2
      RO = SQRT(RO2)
      ROR = 1/RO

      DO B = 1, NC
         dROdV(B) = (V(B)-VRSF*RSF(B)/RSF2)*ROR
      ENDDO

C Get V1 & its derivatives
      DO A=1,NC
         V1(A) = (RS(A)-C(A))*ROR
      ENDDO
      DO A=1,NC
         DUMMY = (RS(A)-C(A))/RO2
         DO B=1,NC
            dV1dV(A,B) = -ROR*dCdV(A,B) - DUMMY*dROdV(B)
         ENDDO
      ENDDO

C Get TF and its derivatives
      CTF = 0.0D0 ! cos(T_N+1)
      DO A = 1,NC
         CTF = CTF + (RF(A)-C(A))*V1(A)*ROR
      ENDDO

      TF = ACOS(CTF) ! POTENTIAL BUG? if TF is > 180
C      IF(ABS(CTF+1).LT.0.001) print*, 'CTF:', CTF
      STF = 1- CTF*CTF
      STF = SQRT(STF)
      
      DUMMY = 0.0D0
      DO A=1,NC
         DUMMY = DUMMY + (RF(A)-C(A))*V1(A)/RO2
      ENDDO

      DO B=1,NC
         dTFdV(B) = -DUMMY*dROdV(B)
         DO A=1,NC
            dTFdV(B) = dTFdV(B) +
     $           ROR*((RF(A)-C(A))*dV1dV(A,B)-dCdV(A,B)*V1(A))
         ENDDO
         dTFdV(B) = - dTFdV(B) / STF
      ENDDO

C Get V2 and its derivatives
      DO A=1,NC
         V2(A) = (RF(A)-C(A)-V1(A)*RO*CTF)*ROR/STF
         DO B=1,NC
            dV2dV(A,B) = -dCdV(A,B)*ROR/STF + V1(A)*dTFdV(B)
     $           - V1(A)*CTF*ROR/STF*dROdV(B)-CTF/STF*dV1dV(A,B)
     $           - ROR*dROdV(B)*V2(A) - CTF/STF*dTFdV(B)*V2(A)
         ENDDO
      ENDDO

      DO I=1,NI
C Get image points Xi      
         STI = SIN(I*TF / (NI+1))
         CTI = COS(I*TF / (NI+1))

         DO A = 1,NC
            XI(I,A) = C(A) + V1(A)*CTI*RO + V2(A)*STI*RO
         ENDDO

C Get derivatives of Xi
         DO A = 1,NC
            DO B= 1, NC
               dXIdV(I,A,B) = dCdV(A,B) - I*V1(A)/(NI+1)*STI*dTFdV(B)*RO +
     $              I*V2(A)/(NI+1)*CTI*dTFdV(B)*RO + 
     $              dV1dV(A,B)*CTI*RO + dV2dV(A,B)*STI*RO +
     $              V1(A)*CTI*dROdV(B)+V2(A)*STI*dROdV(B)
            ENDDO
         ENDDO
      ENDDO
      END SUBROUTINE GETIMGDERVS
